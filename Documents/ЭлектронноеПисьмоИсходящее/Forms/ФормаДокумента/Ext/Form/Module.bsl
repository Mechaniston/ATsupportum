
&НаКлиенте
Перем КоординатыЗаменяемогоСлова, СоответствиеКомандЗаменыСловам;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииИПриЧтенииНаСервере();
		Взаимодействия.УстановитьПредметПоДаннымЗаполнения(Параметры, Предмет);
	КонецЕсли;
	
	Взаимодействия.ЗаполнитьСписокВыбораДляРассмотретьПосле(Элементы.РассмотретьПосле.СписокВыбора);
	
	//подготовить оповещения взаимодействий
	Взаимодействия.ПодготовитьОповещения(ЭтаФорма, Параметры);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, Объект, "СтраницаДополнительныеРеквизиты");
	// Конец СтандартныеПодсистемы.Свойства
	
	//ат+
	РаботаСHTML_Сервер_ат.СоздатьПанелиРаботыСHTML(ЭтаФорма, Элементы.КоманднаяПанельТекстHTML,
		"ОбработчикКомандТекстHTML",, Элементы.ТекстHTML.КонтекстноеМеню);
	ПредставлениеТекстHTML = Объект.ТекстHTML;
	РаботаСHTML_Сервер_ат.СоздатьВременныеФайлыКартинокТекста(Объект, "ТекстHTML", ЭтаФорма,,, ВзаимодействиеОснование);
	
	Элементы.ФормаИспользоватьОтборПоПроектам.Пометка = Ложь;
	Элементы.ФормаИспользоватьОтборПоПроектам.Видимость = Ложь;
	
	Если Объект.Ссылка.Пустая() И ЗначениеЗаполнено(Параметры.Основание) Тогда
		ИсточникМеток = Параметры.Основание;
	Иначе
		ИсточникМеток = Объект.Ссылка;
	КонецЕсли;
	
	Метки_Сервер_ат.ОбновитьМетки(ЭтаФорма, Объект.Ссылка,, Элементы.ФормаИспользоватьОтборПоПроектам.Пометка);
	ОбновитьКоличествоВыбранныхМеток();
	//ат-
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностьюРассмотретьПосле();
	
	//ат+
	РаботаСHTML_Клиент_ат.УстановитьДоступностьПанелейРедактирования(Элементы.КоманднаяПанельТекстHTML,
		Элементы.ТекстHTML, Ложь);
	//ат-
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	ВзаимодействияКлиент.ОтработатьОповещение(ЭтаФорма, ИмяСобытия, Параметр, Источник);

	Если ИмяСобытия = "Запись_ПрисоединенныйФайл" Тогда
		
		Если ТипЗнч(Источник) = Тип("СправочникСсылка.ЭлектронноеПисьмоИсходящееПрисоединенныеФайлы") Тогда
			
			ВложенияТекущиеДанные = Элементы.Вложения.ТекущиеДанные;
			Если ВложенияТекущиеДанные = Неопределено Тогда
				Возврат;
			КонецЕсли;
			РеквизитыФайла = РеквизитыФайла(Источник);
			ЗаполнитьЗначенияСвойств(ВложенияТекущиеДанные, РеквизитыФайла);
			ВложенияТекущиеДанные.РазмерПредставление = 
				ВзаимодействияКлиентСервер.ПолучитьСтроковоеПредставлениеРазмераФайла(РеквизитыФайла.Размер);
			ВложенияТекущиеДанные.ИмяФайла = ?(ПустаяСтрока(РеквизитыФайла.Расширение),
			                                   РеквизитыФайла.Наименование,
			                                   РеквизитыФайла.Наименование + "." + РеквизитыФайла.Расширение);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ОбновленСписокМеток" Тогда
		
		ОбновитьМетки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Перем СтрокаНачало, СтрокаКонец, КолонкаНачало, КолонкаКонец;
	Перем НачалоВыделения, КонецВыделения;
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Документ.ЭлектронноеПисьмоИсходящее.Форма.ФормированиеВнешнейСсылкиНаОбъект") Тогда
		
		Если ФорматСообщения = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст") Тогда
			Элементы.ТекстПисьма.ПолучитьГраницыВыделения(СтрокаНачало, КолонкаНачало, СтрокаКонец, КолонкаКонец);
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			ТекстовыйДокумент.УстановитьТекст(ТекстПисьма);
			СтрокаВставки = ТекстовыйДокумент.ПолучитьСтроку(СтрокаНачало);
			СтрокаВставки = Лев(СтрокаВставки, КолонкаНачало - 1) + ВыбранноеЗначение + Прав(СтрокаВставки,СтрДлина(СтрокаВставки) - КолонкаКонец + 1);
			ТекстовыйДокумент.ЗаменитьСтроку(СтрокаНачало, СтрокаВставки);
			ТекстПисьма = ТекстовыйДокумент.ПолучитьТекст();
		Иначе
			
			Элементы.ТекстПисьмаФорматированныйДокумент.ПолучитьГраницыВыделения(НачалоВыделения, КонецВыделения);
			ТекстПисьмаФорматированныйДокумент.Вставить(НачалоВыделения, ВыбранноеЗначение);
			
		КонецЕсли;
	
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ЖурналДокументов.Взаимодействия.Форма.ПараметрыЭлектронногоПисьма") Тогда
		
		Если ВыбранноеЗначение <> Неопределено И Объект.СтатусПисьма <> ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Отправлено") Тогда
			
			Объект.УведомитьОДоставке          = ВыбранноеЗначение.УведомитьОДоставке;
			Объект.УведомитьОПрочтении         = ВыбранноеЗначение.УведомитьОПрочтении;
			Объект.ВключатьТелоИсходногоПисьма = ВыбранноеЗначение.ВключатьТелоИсходногоПисьма;
			Модифицированность = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.АдреснаяКнига")
		ИЛИ ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.УточнениеКонтактов") Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") И ТипЗнч(ВыбранноеЗначение) <> Тип("Соответствие") Тогда
			Возврат;
		КонецЕсли;
		
		// Получим список адресатов
		СоответствиеТабличныхЧастей = Новый Соответствие;
		СоответствиеТабличныхЧастей.Вставить("Кому", Объект.ПолучателиПисьма);
		СоответствиеТабличныхЧастей.Вставить("Копии", Объект.ПолучателиКопий);
		СоответствиеТабличныхЧастей.Вставить("Скрытые", Объект.ПолучателиСкрытыхКопий);
		СоответствиеТабличныхЧастей.Вставить("Ответ", Объект.ПолучателиОтвета);
		
		ДляПодбора = (Объект.СтатусПисьма <> ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Отправлено"));
		
		// Заполним адресатов
		Если ДляПодбора Тогда
			ЗаполнитьПодобранныхПолучателей(СоответствиеТабличныхЧастей, ВыбранноеЗначение);
		Иначе
			ЗаполнитьУточненныеКонтакты(ВыбранноеЗначение);
		КонецЕсли;
		
		// Установим признак модифицированности
		ВзаимодействияКлиентСервер.ПроверитьЗаполнениеКонтактов(Объект, ЭтаФорма, "ЭлектронноеПисьмоИсходящее");
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииИПриЧтенииНаСервере();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ОчиститьСообщения();
	
	Если Не ВыполняетсяОтправка Тогда
		
		Если ПроверитьЗаполнениеСписковАдресатов() Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	#Если Не ВебКлиент Тогда
		Для Каждого СтрокаТаблицыВложений Из Вложения Цикл
			Если СтрокаТаблицыВложений.Расположение = 2 Тогда
				Данные = Новый ДвоичныеДанные(СтрокаТаблицыВложений.ИмяФайлаНаКомпьютере);
				СтрокаТаблицыВложений.ИмяФайлаНаКомпьютере = ПоместитьВоВременноеХранилище(Данные, "");
				СтрокаТаблицыВложений.Расположение = 4;
			КонецЕсли;
		КонецЦикла;
	#КонецЕсли
	
	Объект.ЕстьВложения = (Вложения.Количество() <> 0);
	
	//ат+
	Элементы.ТекстHTML.Документ.body.innerHTML =
		ПроверкаОрфографии_Клиент_ат.УдалитьИзТекстаHTMLТэгиВыделения(
			Элементы.ТекстHTML.Документ.body.innerHTML);
	Объект.ТекстHTML = Элементы.ТекстHTML.Документ.documentElement.outerHTML;
	Объект.Текст = Элементы.ТекстHTML.Документ.body.innerText;
	//ат-
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, РежимЗаписи, РежимПроведения)
	
	ДокументHTMLТекущегоПисьмаПодготовлен = Ложь;
	
	//Подготовим документ HTML из содержимого форматированного документа
	Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML
		И ТекущийОбъект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Черновик Тогда
		
		ТаблицаСоответствийИменВложенийИдентификаторам.Очистить();
		
		СтруктураВложений = Новый Структура;
		
		//ат+
		//ТекстПисьмаФорматированныйДокумент.ПолучитьHTML(ТекущийОбъект.ТекстHTML, СтруктураВложений);
		//ат-
		
		Для каждого Вложение Из СтруктураВложений Цикл
			
			НоваяСтрока = ТаблицаСоответствийИменВложенийИдентификаторам.Добавить();
			НоваяСтрока.ИмяФайла = Вложение.Ключ;
			НоваяСтрока.ИдентификаторФайлаДляHTML = Новый УникальныйИдентификатор;
			НоваяСтрока.Картинка = Вложение.Значение;
			
		КонецЦикла;
		
		Если ТаблицаСоответствийИменВложенийИдентификаторам.Количество() > 0 Тогда
			
			ДокументHTML = Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(ТекущийОбъект.ТекстHTML);
			Взаимодействия.ЗаменитьИменаКартинокНаИдентификаторыПочтовыхВложенийВHTML(
				ДокументHTML, ТаблицаСоответствийИменВложенийИдентификаторам.Выгрузить());
			
			ДокументHTMLТекущегоПисьмаПодготовлен = Истина;
			
		КонецЕсли;
		
	Иначе
		
		ТекущийОбъект.Текст = ТекстПисьма;
		
	КонецЕсли;
	
	Если НеобходимаОбработкаПисьмаОснования() Тогда
		
		//ат+
		//Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		//	
		//	ТекущийОбъект.ТекстHTML = СформироватьТекстПисьмаВключаяПисьмоОснование(
		//		?(ДокументHTMLТекущегоПисьмаПодготовлен, ДокументHTML, Неопределено), ТекущийОбъект);
		//	ТекущийОбъект.Текст = Взаимодействия.ПолучитьОбычныйТекстИзHTML(ТекущийОбъект.ТекстHTML);
		//	
		//Иначе
		//	ТекущийОбъект.Текст = СформироватьТекстПисьмаВключаяПисьмоОснование(Неопределено, ТекущийОбъект);
		//КонецЕсли;
		//ат-
		
	Иначе
		
		Если ДокументHTMLТекущегоПисьмаПодготовлен Тогда
			
			ТекущийОбъект.ТекстHTML = Взаимодействия.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
			ТекущийОбъект.Текст     = Взаимодействия.ПолучитьОбычныйТекстИзHTML(ТекущийОбъект.ТекстHTML);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыполняетсяОтправка И Не ОтправлятьСообщенияСразу Тогда
		
		ТекущийОбъект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Исходящее;
		
	КонецЕсли;
	
	Если Объект.СтатусПисьма <> ТекущийСтатусПисьма И
		ТекущийОбъект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Исходящее И
		Не ПолучитьФункциональнуюОпцию("ОтправлятьПисьмаВФорматеHTML") И 
		(ТипЗнч(ВзаимодействиеОснование) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") ИЛИ
			ТипЗнч(ВзаимодействиеОснование) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее")) И
		ТипТекстаВходящегоПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.HTML Тогда
		
		ТекущийОбъект.ЕстьВложения = Истина;
		
	КонецЕсли;
	
	ТекущийОбъект.Размер = ОценитьРазмерПисьма();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Взаимодействия.ПередЗаписьюВзаимодействияИзФормы(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Взаимодействия.ПриЗаписиВзаимодействияИзФормы(ТекущийОбъект, Предмет);
	
	//АТ+
	Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		ОбработчикиСобытийДляПочтовыхДокументов_ат.ЭкспортироватьТекстВРССодержаниеЭлектронныхПисем(ТекущийОбъект.Ссылка, ТекущийОбъект.ТекстHTML, Ложь);
		ТекущийОбъект.ТекстHTML = "";
	Иначе
		ОбработчикиСобытийДляПочтовыхДокументов_ат.ЭкспортироватьТекстВРССодержаниеЭлектронныхПисем(ТекущийОбъект.Ссылка, ТекущийОбъект.Текст, Ложь);
		ТекущийОбъект.Текст = "";
	КонецЕсли;
	
	//Уведомления_ат.УстановитьРассмотренностьПисем(Объект, НаКонтроле); //зачем то одно, то другое?!
	Уведомления_ат.ЗаписатьСведенияОКонтролеЭлектронногоПисьма(ТекущийОбъект.Ссылка, НаКонтроле);
	
	Метки_Сервер_ат.ЗаписатьМетки(ЭтаФорма, ТекущийОбъект.Ссылка);
	//ат-
	
	///////////////////////////////////////////////////////

	НайденнаяПапка = Неопределено;
	
	Если Объект.СтатусПисьма <> Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Черновик Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Письмо = ТекущийОбъект.Ссылка;
	
	//Добавим в список удаленных вложений ранее сохраненные картинки, отображаемые в теле форматированного документа
	ТаблицаВложенийКартинокФорматированногоДокумента = Взаимодействия.ПолучитьВложенияПисьмаСНеПустымИД(Письмо);
	Для каждого Вложение Из ТаблицаВложенийКартинокФорматированногоДокумента Цикл
		УдаленныеВложения.Добавить(Вложение.Ссылка);
	КонецЦикла;
	
	// Удалим удаленные вложения
	Для Каждого УдаленноеВложение Из УдаленныеВложения Цикл
		ОбъектВложение = УдаленноеВложение.Значение.ПолучитьОбъект();
		ОбъектВложение.Удалить();
	КонецЦикла;
	
	УдаленныеВложения.Очистить();
	
	Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		Для каждого Вложение Из ТаблицаСоответствийИменВложенийИдентификаторам Цикл
			
			ДвоичныеДанныеКартинки = Вложение.Картинка.ПолучитьДвоичныеДанные();
			АдресКартинкиВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКартинки, УникальныйИдентификатор);
			ПрисоединенныйФайл = УправлениеЭлектроннойПочтой.ЗаписатьВложениеЭлектронногоПисьмаИзВременногоХранилища(
			Письмо,
			АдресКартинкиВоВременномХранилище,
			"_" + СтрЗаменить(Вложение.ИдентификаторФайлаДляHTML, "-", "_"),
			ДвоичныеДанныеКартинки.Размер());
			
			Если ПрисоединенныйФайл <> Неопределено Тогда
				ПрисоединенныйФайлОбъект = ПрисоединенныйФайл.ПолучитьОбъект();
				ПрисоединенныйФайлОбъект.ИДФайлаЭлектронногоПисьма = Вложение.ИдентификаторФайлаДляHTML;
				ПрисоединенныйФайлОбъект.Записать();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НеобходимаОбработкаПисьмаОснования() Тогда
		
		ВложенияПисьмаОснования = Взаимодействия.ПолучитьВложенияПисьмаСНеПустымИД(Объект.ВзаимодействиеОснование);
		
		Для Каждого Вложение ИЗ ВложенияПисьмаОснования Цикл
			
			ДвоичныеДанныеКартинки = ПрисоединенныеФайлы.ПолучитьДвоичныеДанныеФайла(Вложение.Ссылка);
			АдресКартинкиВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКартинки, УникальныйИдентификатор);
			ПрисоединенныйФайл = УправлениеЭлектроннойПочтой.ЗаписатьВложениеЭлектронногоПисьмаИзВременногоХранилища(
			Письмо,АдресКартинкиВоВременномХранилище,Вложение.Наименование, Вложение.Размер);
			Если ПрисоединенныйФайл <> Неопределено Тогда
				ПрисоединенныйФайлОбъект = ПрисоединенныйФайл.ПолучитьОбъект();
				ПрисоединенныйФайлОбъект.ИДФайлаЭлектронногоПисьма = Вложение.ИДФайлаЭлектронногоПисьма;
				ПрисоединенныйФайлОбъект.Записать();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого СтрокаТаблицыВложений Из Вложения Цикл
		
		Размер = 0;
		ИмяФайла = СтрокаТаблицыВложений.ИмяФайла;
		
		Если СтрокаТаблицыВложений.Расположение = 4 Тогда
			// из временного хранилища
			УправлениеЭлектроннойПочтой.ЗаписатьВложениеЭлектронногоПисьмаИзВременногоХранилища(
			Письмо, СтрокаТаблицыВложений.ИмяФайлаНаКомпьютере, ИмяФайла, Размер);
			
		ИначеЕсли СтрокаТаблицыВложений.Расположение = 3 Тогда
			// из файла на сервере
			
		ИначеЕсли СтрокаТаблицыВложений.Расположение = 1 Тогда
			
			УправлениеЭлектроннойПочтой.ЗаписатьВложениеЭлектронногоПисьмаСкопировавВложениеДругогоПисьма(
			Письмо, СтрокаТаблицыВложений.Ссылка, УникальныйИдентификатор);
			
		ИначеЕсли СтрокаТаблицыВложений.Расположение = 0 Тогда
			// перезаписать вложение
			
		КонецЕсли;
		
		СтрокаТаблицыВложений.Расположение = 0;
		
	КонецЦикла;
	
	Если Объект.СтатусПисьма <> ТекущийСтатусПисьма Тогда
		ПрикрепитьВходящееПисьмоОснованиеКакВложениеЕслиНеобходимо(ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьВложения();
	Взаимодействия.УстановитьЗаголовокФормыЭлектронногоПисьма(ЭтаФорма);
	УстановитьЗаголовокКнопкиПоУмолчанию();
	
	Если Объект.СтатусПисьма <> ТекущийСтатусПисьма Тогда
		
		ВзаимодействияВызовСервера.УстановитьПапкуЭлектронногоПисьма(Объект.Ссылка,
			Взаимодействия.ОпределитьПапкуДляПисьмаПоУмолчанию(Объект.Ссылка, Истина));
		ТекущийСтатусПисьма = Объект.СтатусПисьма;
		
	КонецЕсли;
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(Объект.Комментарий);
	  
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ВзаимодействияКлиент.ВзаимодействиеПредметПослеЗаписи(ЭтаФорма,
		Объект,
		ПараметрыЗаписи,
		"ЭлектронноеПисьмоИсходящее");
	
	ОбновитьОтображениеДанных();
	
	//ат+
	ИсточникМеток = Объект.Ссылка;
	Оповестить("ОбновилисьМетки",, ЭтаФорма);
	//ат-
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыОписаниеДополнительноПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьКоличествоВыбранныхМеток();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РассмотретьПослеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВзаимодействияКлиент.ОбработатьВыборВПолеРассмотретьПосле(
		Объект.РассмотретьПосле, ВыбранноеЗначение, СтандартнаяОбработка, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеОтправителяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Объект.УчетнаяЗапись <> ВыбранноеЗначение Тогда
		Объект.УчетнаяЗапись = ВыбранноеЗначение;
		ЭлементСписка = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Если ЭлементСписка <> Неопределено Тогда
			СтандартнаяОбработка = Ложь;
			Объект.ОтправительПредставление = ЭлементСписка.Представление;
		КонецЕсли;
		
		//ат+
		ВставитьПодпись();
		//ат-
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПолучателейПисьмаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	Попытка
		Результат = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(Элемент.ТекстРедактирования);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), , Элемент.Имя, "Объект");
		Возврат;
	КонецПопытки;
	
	Если Объект.СтатусПисьма <> ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Отправлено") Тогда
		
		Если Элемент.Имя = "СписокПолучателейПисьма" Тогда
			ГруппаПодбора = НСтр("ru = 'Кому'");
		ИначеЕсли Элемент.Имя = "СписокПолучателейКопий" Тогда
			ГруппаПодбора = НСтр("ru = 'Копии'");
		ИначеЕсли Элемент.Имя = "СписокПолучателейСкрытыхКопий" Тогда
			ГруппаПодбора = НСтр("ru = 'Скрытые'");
		КонецЕсли;
		
		РедактироватьСписокПолучателей(Истина, ГруппаПодбора);
		
	Иначе
		
		РедактироватьСписокПолучателей(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПолучателейОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	ИмяЭлемента = Элемент.Имя;
	ИмяСвязаннойТаблицы = ПолучитьИмяСвязаннойТаблицы(ИмяЭлемента);
	
	Если Не ПустаяСтрока(Текст) Тогда
		
		Модифицированность = Истина;
		
		ТекстДляПолученияДанныхВыбора = "";
		
		ПодстрокиВведенногоТекста = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ";");
		ПодстрокиДанныхОбъекта    = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Объект[ИмяЭлемента], ";");
		
		Если (ПодстрокиВведенногоТекста.Количество() < ПодстрокиДанныхОбъекта.Количество()) ИЛИ
			(ПодстрокиВведенногоТекста.Количество() > (ПодстрокиДанныхОбъекта.Количество()+1)) Тогда
			
			ДанныеВыбора = Новый СписокЗначений;
			ДанныеВыбора.Добавить(Текст);
			Объект[ИмяЭлемента] = Текст;
			ПреобразоватьИменаПолучателей(Объект[ИмяЭлемента], ИмяСвязаннойТаблицы, Элемент.Имя);
			Возврат;
			
		КонецЕсли;
		
		Для Инд = 0 По (ПодстрокиВведенногоТекста.Количество()-1) Цикл
			Если Инд > (ПодстрокиДанныхОбъекта.Количество()-1) ИЛИ
				ПодстрокиВведенногоТекста[Инд] <> ПодстрокиДанныхОбъекта[инд] Тогда
				ТекстДляПолученияДанныхВыбора = 
					СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ПодстрокиВведенногоТекста[инд], " ");
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ПустаяСтрока(ТекстДляПолученияДанныхВыбора) Тогда
			
			ДанныеВыбора = Новый СписокЗначений;
			ДанныеВыбора.Добавить(Текст);
			Возврат;
		Иначе
			Текст = ТекстДляПолученияДанныхВыбора;
		КонецЕсли;
		
		ДанныеВыбора = ПолучитьДанныеВыбораДляЭлектронногоПисьма(Текст,Инд);
		
		Если ДанныеВыбора.Количество() = 1 Тогда
			
			ПоместитьНайденногоАдресатаВСписок(ИмяСвязаннойТаблицы, ДанныеВыбора.Получить(0).Значение,Инд)
			
		ИначеЕсли ДанныеВыбора.Количество() = 0 Тогда
			
			ДанныеВыбора = Новый СписокЗначений;
			ДанныеВыбора.Добавить(Текст);
			Попытка
			Результат = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(Текст);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),
					,
					Элемент.Имя,"Объект");
				Возврат;
			КонецПопытки;
			Если Результат.Количество() = 0 Тогда
				Возврат;
			Иначе 
				ПоместитьНайденногоАдресатаВСписок(ИмяСвязаннойТаблицы, Результат[0], Инд);
			КонецЕсли;
			
		Иначе
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Объект[ИмяЭлемента] = "";
		Объект[ИмяСвязаннойТаблицы].Очистить();
		ВзаимодействияКлиентСервер.ПроверитьЗаполнениеКонтактов(Объект, ЭтаФорма, "ЭлектронноеПисьмоИсходящее");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПолучателейАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяЭлемента = Элемент.Имя;
	ТекстДляПолученияДанныхВыбора = "";
	
	ПодстрокиВведенногоТекста = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ";");
	ПодстрокиДанныхОбъекта    = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Объект[ИмяЭлемента], ";");
	
	Для Инд = 0 По (ПодстрокиВведенногоТекста.Количество()-1) Цикл
		Если Инд > (ПодстрокиДанныхОбъекта.Количество()-1) ИЛИ
			ПодстрокиВведенногоТекста[Инд] <> ПодстрокиДанныхОбъекта[инд] Тогда
			ТекстДляПолученияДанныхВыбора = 
				СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ПодстрокиВведенногоТекста[инд], " ");
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстДляПолученияДанныхВыбора) Тогда
		Возврат;
	Иначе
		Текст = ТекстДляПолученияДанныхВыбора;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ПолучитьДанныеВыбораДляЭлектронногоПисьма(Текст, Инд);

КонецПроцедуры

&НаКлиенте
Процедура СписокПолучателейОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		ПоместитьНайденногоАдресатаВСписок(
			ПолучитьИмяСвязаннойТаблицы(Элемент.Имя),
			ВыбранноеЗначение,
			ВыбранноеЗначение.ИндексВСпискеПолучателей);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьНайденногоАдресатаВСписок(ИмяСпискаАдресатов, ДанныеАдресата, ИндексВСпискеПолучателей)

	Модифицированность = Истина;
	
	Если Объект[ИмяСпискаАдресатов].Количество() > ИндексВСпискеПолучателей Тогда
		СтрокаАдресата = Объект[ИмяСпискаАдресатов][ИндексВСпискеПолучателей];
	Иначе
		СтрокаАдресата = Объект[ИмяСпискаАдресатов].Добавить();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрокаАдресата,ДанныеАдресата);
	
	СписокАдресатов = ВзаимодействияКлиентСервер.ПолучитьПредставлениеСпискаАдресатов(Объект[ИмяСпискаАдресатов],Ложь);
	Если ИмяСпискаАдресатов = "ПолучателиПисьма" Тогда
		Объект.СписокПолучателейПисьма = СписокАдресатов;
	ИначеЕсли ИмяСпискаАдресатов = "ПолучателиКопий" Тогда
		Объект.СписокПолучателейКопий = СписокАдресатов;
	ИначеЕсли ИмяСпискаАдресатов = "ПолучателиСкрытыхКопий" Тогда
		Объект.СписокПолучателейСкрытыхКопий= СписокАдресатов;
	КонецЕсли;
	
	ВзаимодействияКлиентСервер.ПроверитьЗаполнениеКонтактов(Объект, ЭтаФорма, "ЭлектронноеПисьмоИсходящее");

КонецПроцедуры

&НаКлиенте
Процедура ТекстПисьмаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ВзаимодействияКлиент.ПолеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстВходящегоПисьмаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ВзаимодействияКлиент.ПолеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Вложения

&НаКлиенте
Процедура ВложенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьВложениеВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если Объект.СтатусПисьма <> ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Черновик") Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	МассивИменФайлов = Новый Массив;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл") И 
		ПараметрыПеретаскивания.Значение.ЭтоФайл() = Истина Тогда
		
		МассивИменФайлов.Добавить(ПараметрыПеретаскивания.Значение.ПолноеИмя);
		
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		
		Если ПараметрыПеретаскивания.Значение.Количество() >= 1 И
			ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Файл") Тогда
			
			Для Каждого ФайлПринятый Из ПараметрыПеретаскивания.Значение Цикл
				Если ТипЗнч(ФайлПринятый) = Тип("Файл") И ФайлПринятый.ЭтоФайл() Тогда
					МассивИменФайлов.Добавить(ФайлПринятый.ПолноеИмя);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого ВыбранныйФайл Из МассивИменФайлов Цикл
		
		НоваяСтрока = Вложения.Добавить();
		НоваяСтрока.Расположение = 2;
		НоваяСтрока.ИмяФайлаНаКомпьютере = ВыбранныйФайл;
		
		ИмяФайла   = "";
		Расширение = "";
		ВзаимодействияКлиентСервер.ПолучитьКаталогИИмяФайла(ВыбранныйФайл, "", ИмяФайла);
		НоваяСтрока.ИмяФайла = ИмяФайла;
		
		Расширение                      = ВзаимодействияКлиентСервер.ПолучитьРасширениеФайла(ИмяФайла);
		НоваяСтрока.ИндексКартинки      = ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(Расширение);
		Файл                            = Новый Файл(ВыбранныйФайл);
		НоваяСтрока.Размер              = Файл.Размер();
		НоваяСтрока.РазмерПредставление = ВзаимодействияКлиентСервер.ПолучитьСтроковоеПредставлениеРазмераФайла(НоваяСтрока.Размер);
		
	КонецЦикла;
	
	ВидимостьДоступностьЭлементовВложений();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОтправитьПереслатьВыполнить(Команда)
	
	ОчиститьСообщения();
	
	Если Объект.СтатусПисьма = ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Отправлено") Тогда
		ПереслатьВыполнить();
	Иначе
		
		Если ПроверитьЗаполнениеСписковАдресатов() Тогда
			Возврат;
		КонецЕсли;
		
		Если Объект.ПолучателиПисьма.Количество() = 0 Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Необходимо указать хотя бы одного получателя письма.'"),, "Объект.СписокПолучателейПисьма");
			Возврат;
			
		КонецЕсли;
		
		ОтправитьВыполнить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура HTML(Команда)
	
	Если ФорматСообщения <> ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		ФорматСообщения = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML");
		ФорматСообщенияПриИзменении();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныйТекст(Команда)
	
	Если ФорматСообщения <> ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст") Тогда
		
		ФорматСообщения = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст");
		ФорматСообщенияПриИзменении();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТекстПисьмаОснования(Команда)
	
	Элементы.ОтображатьТекстПисьмаОснования.Пометка = Не Элементы.ОтображатьТекстПисьмаОснования.Пометка;
	Элементы.ГруппаВходящее.Видимость = Элементы.ОтображатьТекстПисьмаОснования.Пометка;
	Объект.ОтображатьТелоИсходногоПисьма = Не Объект.ОтображатьТелоИсходногоПисьма;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьКонтакты(Команда)
	
	Если Объект.СтатусПисьма <> ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Отправлено") Тогда
		
		РедактироватьСписокПолучателей(Истина, НСтр("ru = 'Копии'"));
		
	Иначе
		
		РедактироватьСписокПолучателей(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыПисьма(Команда)
	
	//AT+
	ТекстЗаголовкиИнтернета = Новый ТекстовыйДокумент;
	ТекстЗаголовкиИнтернета.ДобавитьСтроку("Идентификатор сообщения:  " + Объект.ИдентификаторСообщения);
	ТекстЗаголовкиИнтернета.ДобавитьСтроку("Идентификатор основания:  " + Объект.ИдентификаторОснования);
	ТекстЗаголовкиИнтернета.ДобавитьСтроку("Идентификаторы оснований: " + 
		ПолучитьПредставлениеИдентификаторовОснований(Объект.ИдентификаторыОснований));
	
	ТекстЗаголовкиИнтернета.ДобавитьСтроку("");
	ТекстЗаголовкиИнтернета.ДобавитьСтроку("");
	ТекстЗаголовкиИнтернета.ДобавитьСтроку(
		РаботаСHTML_КлиентСервер_ат.ПреобразоватьОбычныйТекстВHTML(
			Элементы.ТекстПисьма.Документ.documentElement.outerHTML));
	//AT-
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Создано", Объект.Дата);
	СтруктураПараметры.Вставить("Отправлено", Объект.ДатаОтправления);
	СтруктураПараметры.Вставить("УведомитьОДоставке", Объект.УведомитьОДоставке);
	СтруктураПараметры.Вставить("УведомитьОПрочтении", Объект.УведомитьОПрочтении);
	СтруктураПараметры.Вставить("ЗаголовкиИнтернета", ТекстЗаголовкиИнтернета);
	СтруктураПараметры.Вставить("Письмо", Объект.Ссылка);
	СтруктураПараметры.Вставить("ТипПисьма", "ЭлектронноеПисьмоИсходящее");
	СтруктураПараметры.Вставить("Кодировка", Объект.Кодировка);
	СтруктураПараметры.Вставить("ВнутреннийНомер", Объект.Номер);
	СтруктураПараметры.Вставить("ВключатьТелоИсходногоПисьма", Объект.ВключатьТелоИсходногоПисьма);
	СтруктураПараметры.Вставить("УчетнаяЗапись", Объект.УчетнаяЗапись);
	
	ОткрытьФорму("ЖурналДокументов.Взаимодействия.Форма.ПараметрыЭлектронногоПисьма", СтруктураПараметры, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьВнешнююСсылкуНаОбъектИнформационнойБазы(Команда)
	
	ОткрытьФорму("Документ.ЭлектронноеПисьмоИсходящее.Форма.ФормированиеВнешнейСсылкиНаОбъект",,ЭтаФорма);
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура НаКонтролеПриИзменении()
	
	УправлениеДоступностьюРассмотретьПосле();
	
	Объект.Рассмотрено = НЕ НаКонтроле;
	Модифицированность = Истина;
	
	НаКонтролеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура НаКонтролеПриИзмененииНаСервере()
	
	Если НаКонтроле Тогда
		Объект.РассмотретьПосле = Уведомления_ат.ПолучитьДатуРассмотренияПисьма();
	Иначе
		Объект.РассмотретьПосле = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция   ПроверитьЗаполнениеСписковАдресатов()

	Отказ = Ложь;
	
	Попытка
		Результат = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(Объект.СписокПолучателейПисьма);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), , "Объект.СписокПолучателейПисьма");
		Отказ = Истина;
	КонецПопытки;
	
	Попытка
		Результат = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(Объект.СписокПолучателейКопий);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), , "Объект.СписокПолучателейКопий");
		Отказ = Истина;
	КонецПопытки;
	
	Попытка
		Результат = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(Объект.СписокПолучателейСкрытыхКопий);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), , "Объект.СписокПолучателейСкрытыхКопий");
		Отказ = Истина;
	КонецПопытки;
	
	Возврат Отказ;

КонецФункции

&НаСервере
Функция   НеобходимаОбработкаПисьмаОснования()
	
	Возврат ВыполняетсяОтправка И Объект.ВключатьТелоИсходногоПисьма
		И Объект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Черновик
		И Объект.ВзаимодействиеОснование <> Неопределено И НЕ Объект.ВзаимодействиеОснование.Пустая();
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////
//  Управление доступностью элементов формы

&НаСервере
Процедура ОпределитьВидимостьДоступностьЭлементовВЗависимостиОтСтатусаПисьма()
	
	Если Объект.СтатусПисьма <> Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Отправлено Тогда
		
		УправлениеЭлектроннойПочтой.ПолучитьДоступныеУчетныеЗаписиДляОтправки(
			Элементы.ОтправительПредставление.СписокВыбора,ДоступныеДляОтправкиУчетныеЗаписи);
		
	Иначе
		
		Элементы.ОтправительПредставление.ТолькоПросмотр            = Истина;
		Элементы.СписокПолучателейПисьма.РедактированиеТекста       = Ложь;
		Элементы.СписокПолучателейКопий.РедактированиеТекста        = Ложь;
		Элементы.СписокПолучателейСкрытыхКопий.РедактированиеТекста = Ложь;
		
	КонецЕсли;
	
	Если Объект.СтатусПисьма <> Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Черновик Тогда
		
		Если Вложения.Количество() > 0 Тогда
			Элементы.СтраницыВложения.ТекущаяСтраница  = Элементы.СтраницаРедактирование;
			Элементы.ДобавитьВложение.Доступность      = Ложь;
			Элементы.УдалитьВложение.Доступность       = Ложь;
			Элементы.ДобавитьВложениеСкрепка.Видимость = Ложь;
		Иначе
			Элементы.СтраницыВложения.Видимость = Ложь;
		КонецЕсли;
		
		Если Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTML
			ИЛИ Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTMLСКартинками Тогда
			ТекстПисьма = Объект.ТекстHTML;
			ТекстПисьма = Взаимодействия.ОбработатьТекстHTML(Объект.Ссылка);
			Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеHTMLДокумента;
			Элементы.ТекстПисьма.ТолькоПросмотр = Ложь;
		Иначе
			ТекстПисьма = Объект.Текст;
			Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
			Элементы.ТекстПисьма.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		Если Вложения.Количество() > 0 Тогда
			Элементы.СтраницыВложения.ТекущаяСтраница = Элементы.СтраницаРедактирование;
			Элементы.Вложения.ВысотаВСтрокахТаблицы   = 2;
		КонецЕсли;
		
		//Если Объект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Исходящее И 
		//	НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		//	Элементы.Отправить.Доступность = Ложь;
		//КонецЕсли;
		
	Иначе
		
		Элементы.СтраницыВложения.ТекущаяСтраница  = Элементы.СтраницаРедактирование;
		Если Вложения.Количество() > 0 Тогда
			Элементы.Вложения.ВысотаВСтрокахТаблицы    = 2;
		Иначе
			Элементы.Вложения.ВысотаВСтрокахТаблицы    = 1;
		КонецЕсли;
		
		ОпределитьСпособРедактированияПисьма();
		
	КонецЕсли;

КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////
// Работа с вложениями

&НаКлиенте
Процедура ВложенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ДобавитьВложениеВыполнить();

КонецПроцедуры

&НаКлиенте
Процедура ВложенияПередУдалением(Элемент, Отказ)
	
	Если Объект.СтатусПисьма = ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Черновик") Тогда
		УдалитьВложениеВыполнить();
	КонецЕсли;
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеВыполнить()
	
	#Если Не ВебКлиент Тогда
		
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Диалог.МножественныйВыбор = Истина;
		Если Не Диалог.Выбрать() Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого ВыбранныйФайл Из Диалог.ВыбранныеФайлы Цикл
			НоваяСтрока = Вложения.Добавить();
			НоваяСтрока.Расположение = 2;
			НоваяСтрока.ИмяФайлаНаКомпьютере = ВыбранныйФайл;

			ИмяФайла   = "";
			Расширение = "";
			ВзаимодействияКлиентСервер.ПолучитьКаталогИИмяФайла(ВыбранныйФайл, "", ИмяФайла);
			НоваяСтрока.ИмяФайла = ИмяФайла;

			Расширение                      = ВзаимодействияКлиентСервер.ПолучитьРасширениеФайла(ИмяФайла);
			НоваяСтрока.ИндексКартинки      = ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(Расширение);
			Файл                            = Новый Файл(ВыбранныйФайл);
			НоваяСтрока.Размер              = Файл.Размер();
			НоваяСтрока.РазмерПредставление = ВзаимодействияКлиентСервер.ПолучитьСтроковоеПредставлениеРазмераФайла(НоваяСтрока.Размер);
		КонецЦикла;
		
	#Иначе

		Адрес = "";
		ВыбранныйФайл = "";
		Если Не ПоместитьФайл(Адрес, "", ВыбранныйФайл, Истина, УникальныйИдентификатор) Тогда
			Возврат;
		КонецЕсли;

		НоваяСтрока = Вложения.Добавить();
		НоваяСтрока.Расположение = 4;
		НоваяСтрока.ИмяФайлаНаКомпьютере = Адрес;
		НоваяСтрока.ИмяФайла = ВыбранныйФайл;
		
		Расширение = ВзаимодействияКлиентСервер.ПолучитьРасширениеФайла(ВыбранныйФайл);
		НоваяСтрока.ИндексКартинки = ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(Расширение);
		
		ОбновитьОтображениеДанных();
		
	#КонецЕсли
	
	ВидимостьДоступностьЭлементовВложений();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьДоступностьЭлементовВложений()
	
	Если Объект.СтатусПисьма = ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Отправлено") Тогда
		Возврат;
	КонецЕсли;
	
	Если Вложения.Количество() > 0 Тогда
		Элементы.СтраницыВложения.ТекущаяСтраница = Элементы.СтраницаРедактирование;
		Элементы.Вложения.ВысотаВСтрокахТаблицы    = 2;
	Иначе
		Если Объект.СтатусПисьма <> ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Черновик") Тогда
			Элементы.СтраницыВложения.ТекущаяСтраница = Элементы.СтраницаДобавить;
		КонецЕсли;
		Элементы.Вложения.ВысотаВСтрокахТаблицы    = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВложениеВыполнить()

	ДобавитьВложениеВСписокУдаленных();
	
	текДанные = Элементы.Вложения.ТекущиеДанные;
	Если текДанные <> Неопределено Тогда
		Индекс = Вложения.Индекс(текДанные);
		Вложения.Удалить(Индекс);
		ОбновитьОтображениеДанных();
	КонецЕсли;
	
	ВидимостьДоступностьЭлементовВложений();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложениеВСписокУдаленных()
	
	текДанные = Элементы.Вложения.ТекущиеДанные;
	Если (текДанные <> Неопределено) И (текДанные.Расположение = 0) Тогда
		УдаленныеВложения.Добавить(текДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВложениеВыполнить()
	
	текДанные = Элементы.Вложения.ТекущиеДанные;
	Если текДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если (текДанные.Расположение = 0) ИЛИ (текДанные.Расположение = 1) Тогда
		
		УправлениеЭлектроннойПочтойКлиент.ОткрытьВложение(текДанные.Ссылка, УникальныйИдентификатор);
		
	ИначеЕсли текДанные.Расположение = 2 Тогда
		
		ПутьКФайлу = текДанные.ИмяФайлаНаКомпьютере;
		#Если Не ВебКлиент Тогда
			ЗапуститьПриложение("""" + ПутьКФайлу + """");
		#Иначе
			ПолучитьФайл(ПутьКФайлу, текДанные.ИмяФайла, Истина);
		#КонецЕсли
		
	ИначеЕсли текДанные.Расположение = 4 Тогда
		
		ПутьКФайлу = текДанные.ИмяФайлаНаКомпьютере;
		#Если Не ВебКлиент Тогда
			Если ЭтоАдресВременногоХранилища(текДанные.ИмяФайлаНаКомпьютере) Тогда
				ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
				СоздатьКаталог(ИмяВременнойПапки);
				ПутьКФайлу = ИмяВременнойПапки + "\" + текДанные.ИмяФайла;
				ДвоичныеДанные = ПолучитьИзВременногоХранилища(текДанные.ИмяФайлаНаКомпьютере);
				ДвоичныеДанные.Записать(ПутьКФайлу);
			КонецЕсли;
			ЗапуститьПриложение("""" + ПутьКФайлу + """");
		#Иначе
			ПолучитьФайл(ПутьКФайлу, текДанные.ИмяФайла, Истина);
		#КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВложения(Параметры = Неопределено)
	
	Если Объект.Ссылка.Пустая() И Параметры <> Неопределено Тогда
		
		Если Параметры.Свойство("Основание") И 
			ТипЗнч(Параметры.Основание) = Тип("Структура") И
			Параметры.Основание.Команда = "Переслать" Тогда
			
			табВложения = УправлениеЭлектроннойПочтой.ПолучитьВложенияЭлектронногоПисьма(Параметры.Основание.Основание, Истина);
			Для Каждого СтрокаТаблицыВложений Из табВложения Цикл
				Если ПустаяСтрока(СтрокаТаблицыВложений.ИДФайлаЭлектронногоПисьма) Тогда
					НоваяСтрока = Вложения.Добавить();
					НоваяСтрока.Ссылка              = СтрокаТаблицыВложений.Ссылка;
					НоваяСтрока.ИмяФайла            = СтрокаТаблицыВложений.ИмяФайла;
					НоваяСтрока.ИндексКартинки      = СтрокаТаблицыВложений.ИндексКартинки;
					НоваяСтрока.Размер              = СтрокаТаблицыВложений.Размер;
					НоваяСтрока.РазмерПредставление = СтрокаТаблицыВложений.РазмерПредставление;
					НоваяСтрока.Расположение        = 1;
					//ат+
					НоваяСтрока.ПометкаУдаления = СтрокаТаблицыВложений.ПометкаУдаления;
					//ат-
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		Вложения.Очистить();
		табВложения = УправлениеЭлектроннойПочтой.ПолучитьВложенияЭлектронногоПисьма(Объект.Ссылка, Истина);
		Для Каждого СтрокаТаблицыВложений Из табВложения Цикл
			Если ПустаяСтрока(СтрокаТаблицыВложений.ИДФайлаЭлектронногоПисьма) Тогда
				НоваяСтрока = Вложения.Добавить();
				НоваяСтрока.Ссылка              = СтрокаТаблицыВложений.Ссылка;
				НоваяСтрока.ИмяФайла            = СтрокаТаблицыВложений.ИмяФайла;
				НоваяСтрока.ИндексКартинки      = СтрокаТаблицыВложений.ИндексКартинки;
				НоваяСтрока.Размер              = СтрокаТаблицыВложений.Размер;
				НоваяСтрока.РазмерПредставление = СтрокаТаблицыВложений.РазмерПредставление;
				НоваяСтрока.ПодписанЭЦП         = СтрокаТаблицыВложений.ПодписанЭЦП;
				НоваяСтрока.Расположение        = 0;
				//ат+
				НоваяСтрока.ПометкаУдаления = СтрокаТаблицыВложений.ПометкаУдаления;
				//ат-
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрикрепитьВходящееПисьмоОснованиеКакВложениеЕслиНеобходимо(ТекущийОбъект)
	
	Если ТекущийОбъект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Исходящее И
		Не ПолучитьФункциональнуюОпцию("ОтправлятьПисьмаВФорматеHTML") И 
		(ТипЗнч(ВзаимодействиеОснование) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") ИЛИ
		ТипЗнч(ВзаимодействиеОснование) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее")) И
		ТипТекстаВходящегоПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.HTML Тогда
		
		Если ТипЗнч(ВзаимодействиеОснование) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") Тогда
			ТекстHTMLВходящее = Взаимодействия.СформироватьТекстHTMLДляВходящегоПисьма(ВзаимодействиеОснование, Истина, Истина);
		Иначе
			ТекстHTMLВходящее = Взаимодействия.СформироватьТекстHTMLДляИсходящегоПисьма(ВзаимодействиеОснование, Истина, Истина);
		КонецЕсли;
		
		ВременныйКаталог   = КаталогВременныхФайлов();
		ИмяФайла = ВременныйКаталог + НСтр("ru = 'Пересылаемое сообщение'") + ".htm";
		ФайлИсходноеСообщение = Новый ЗаписьТекста(ИмяФайла,КодировкаТекста.UTF16);
		ФайлИсходноеСообщение.Записать(ТекстHTMLВходящее);
		ФайлИсходноеСообщение.Закрыть();
		ДвоичныеДанные       = Новый ДвоичныеДанные(ИмяФайла);
		АдресФайлаВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		
		ПрисоединенныеФайлы.ДобавитьФайл(
		ТекущийОбъект.Ссылка,
		НСтр("ru = 'Пересылаемое сообщение'"),
		"html",
		,
		,
		АдресФайлаВХранилище,
		"");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваВложения(Команда)
	
	ТекущиеДанные = Элементы.Вложения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийИндексВКоллекции = Вложения.Индекс(ТекущиеДанные);
	
	Если ТекущиеДанные.Ссылка = Неопределено Тогда
		Результат = Вопрос(НСтр("ru = 'Свойства файла доступны только после его записи. Записать?'"),РежимДиалогаВопрос.ДаНет);
		Если Результат = КодВозвратаДиалога.Да Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекущиеДанные = Вложения.Получить(ТекущийИндексВКоллекции);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элементы.Вложения.ТекущаяСтрока = ТекущиеДанные.ПолучитьИдентификатор();
		
	ФайлДоступенДляРедактирования = 
		(Объект.СтатусПисьма = ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Черновик"));
	ПараметрыФормы = Новый Структура("ПрисоединенныйФайл, ТолькоПросмотр", 
		ТекущиеДанные.Ссылка,НЕ ФайлДоступенДляРедактирования);
	ОткрытьФорму("ОбщаяФорма.ПрисоединенныйФайл", ПараметрыФормы,, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////
// Формирование текста письма

&НаСервере
Функция   СформироватьТекстПисьмаВключаяПисьмоОснование(ДокументHTMLТекущееРедактирование, ТекущийОбъект)
	
	Выборка = Взаимодействия.ПолучитьДанныеПисьмаОснования(Объект.ВзаимодействиеОснование);
	
	Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст Тогда
		
		Возврат СформироватьИсходящееОбычныйТекст(Выборка, ТекущийОбъект);
		
	Иначе
		
		Возврат СформироватьИсходящееHTML(Выборка, ДокументHTMLТекущееРедактирование, ТекущийОбъект);
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция   СформироватьИсходящееHTML(Выборка, ДокументHTMLТекущееРедактирование, ТекущийОбъект)
	
	//ат+
	Если Выборка.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст Тогда
		
		ОбычныйТекстПисьма = ОбработчикиСобытийДляПочтовыхДокументов_ат.ПолучитьТексПисьмаИзРегистраСодержаниеЭлектронныхПисем(ТекущийОбъект.ВзаимодействиеОснование);
		ДокументHTML = Взаимодействия.ПолучитьДокументHTMLИзОбычногоТекста(ОбычныйТекстПисьма);
		
	Иначе
		
		ТекстHTMLПисьма = ОбработчикиСобытийДляПочтовыхДокументов_ат.ПолучитьТексПисьмаИзРегистраСодержаниеЭлектронныхПисем(ТекущийОбъект.ВзаимодействиеОснование);
		ДокументHTML = Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(ТекстHTMLПисьма);
		
	КонецЕсли;
	//ат-
	
	ЭлементТелоПисьма = ДокументHTML.Тело;
	Если ЭлементТелоПисьма = Неопределено Тогда
		Если ДокументHTMLТекущееРедактирование = Неопределено Тогда
			Возврат ТекущийОбъект.ТекстHTML;
		Иначе
			Возврат Взаимодействия.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTMLТекущееРедактирование);
		КонецЕсли
	КонецЕсли;
	
	Если ДокументHTMLТекущееРедактирование = Неопределено Тогда
		ДокументHTMLТекущееРедактирование = Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(ТекущийОбъект.ТекстHTML);
	КонецЕсли;
	
	МассивДочернихУзловТела = Взаимодействия.ПолучитьМассивДочернихУзловСодержащихHTML(ЭлементТелоПисьма);
	
	//Добавляем текст отредактированный в поле форматированного документа
	Если ДокументHTMLТекущееРедактирование.Тело <> Неопределено Тогда
		Для каждого ДочернийУзел Из ДокументHTMLТекущееРедактирование.Тело.ДочерниеУзлы Цикл
			
			ЭлементТелоПисьма.ДобавитьДочерний(ДокументHTML.ИмпортироватьУзел(ДочернийУзел, Истина));
			
		КонецЦикла;
	КонецЕсли;
	
	ЭлементDIV = Взаимодействия.ДобавитьЭлементСАтрибутами(
		ЭлементТелоПисьма,
		"div",
		Новый Структура("style", "border:none;border-left:solid blue 1.5pt;padding:0cm 0cm 0cm 4.0pt"));
		
	Для Каждого ДочернийУзел Из МассивДочернихУзловТела Цикл
		ЭлементDIV.ДобавитьДочерний(ДочернийУзел);
	КонецЦикла;

	//Подготовим шапку письма основания
	//Горизонтальный разделитель
	ЭлементHR = Взаимодействия.ДобавитьЭлементСАтрибутами(
		ЭлементDIV,
		"hr",
		Новый Структура("size, width, align, tabindex", "2", "100%", "center", "-1"));
	Взаимодействия.ВставитьЭлементHTMLПервымДочернимЭлементом(ЭлементDIV, ЭлементHR, МассивДочернихУзловТела);
	
	//Данные письма основания
	ЭлементШрифт = Взаимодействия.СформироватьЭлементДанныхШапкиПисьма(ЭлементDIV, Выборка);
	Взаимодействия.ВставитьЭлементHTMLПервымДочернимЭлементом(ЭлементDIV, ЭлементШрифт, МассивДочернихУзловТела);
	
	Возврат Взаимодействия.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
КонецФункции

&НаСервере
Функция   СформироватьИсходящееОбычныйТекст(ВыборкаДанныеВходящегоПисьма, ТекущийОбъект)

	//Формирование шапки входящего письма
	СтрокаШапка = НСтр("ru = '-----Пересылаемое сообщение-----'");
	
	СтрокаШапка = СтрокаШапка + Символы.ПС+ НСтр("ru = 'От'") + ": "+ ВыборкаДанныеВходящегоПисьма.ОтправительПредставление +
		?(ВыборкаДанныеВходящегоПисьма.ИмяОбъектаМетаданных = "ЭлектронноеПисьмоВходящее",
		"[" + ВыборкаДанныеВходящегоПисьма.ОтправительАдрес +"]",
		"");
		
	СтрокаШапка = СтрокаШапка + Символы.ПС+ НСтр("ru = 'Отправлено'") + ": "+ Формат(ВыборкаДанныеВходящегоПисьма.Дата,"ДФ='dd MMMM yyyy'");
	
	СтрокаШапка = СтрокаШапка + Символы.ПС+ НСтр("ru = 'Кому'") + ": " +
		Взаимодействия.ПолучитьПредставленияПолучателейВходящегоПисьма(
		ВыборкаДанныеВходящегоПисьма.ПолучателиПисьма.Выгрузить());
		
	ТаблицаПолучателейКопий = ВыборкаДанныеВходящегоПисьма.ПолучателиКопий.Выгрузить();
	
	Если ТаблицаПолучателейКопий.Количество() > 0 Тогда
		СтрокаШапка = СтрокаШапка + Символы.ПС+ НСтр("ru = 'Копии'") + ": " + 
			Взаимодействия.ПолучитьПредставленияПолучателейВходящегоПисьма(ТаблицаПолучателейКопий);
	КонецЕсли;
	
	СтрокаШапка = СтрокаШапка + Символы.ПС+ НСтр("ru = 'Тема'") + ": " + ВыборкаДанныеВходящегоПисьма.Тема;
	
	//Преобразование к обычному тексту текста HTML если это необходимо
	Если ВыборкаДанныеВходящегоПисьма.ТипТекста <> Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст Тогда
		//@comm
		ТекстHTMLВходящегоПисьма = ОбработчикиСобытийДляПочтовыхДокументов_ат.ПолучитьТексПисьмаИзРегистраСодержаниеЭлектронныхПисем(ТекущийОбъект.ВзаимодействиеОснование);
		ТекстВходящегоПисьма =  Взаимодействия.ПолучитьОбычныйТекстИзHTML(ВыборкаДанныеВходящегоПисьма.ТекстHTML);
		// Оригинал БСП
		//ТекстВходящегоПисьма =  Взаимодействия.ПолучитьОбычныйТекстИзHTML(ВыборкаДанныеВходящегоПисьма.ТекстHTML);
	Иначе
		// Поменять на загрузку из регистра
		//ТекстВходящегоПисьма = ВыборкаДанныеВходящегоПисьма.Текст
		////@comm
		ТекстВходящегоПисьма =  ОбработчикиСобытийДляПочтовыхДокументов_ат.ПолучитьТексПисьмаИзРегистраСодержаниеЭлектронныхПисем(ТекущийОбъект.ВзаимодействиеОснование);
	КонецЕсли;
	// оригинал БСП
	//Возврат ТекущийОбъект.Текст + Символы.ПС + Символы.ПС + СтрокаШапка + Символы.ПС + Символы.ПС + ТекстВходящегоПисьма;
	
	Возврат ТекстПисьма + Символы.ПС + Символы.ПС + СтрокаШапка + Символы.ПС + Символы.ПС + ТекстВходящегоПисьма;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Прочее

// Определяет способ редактирования письма и
// отображает текст письма согласно способу редактирования
&НаСервере
Процедура ОпределитьСпособРедактированияПисьма()

	Если Объект.ТипТекста.Пустая() Тогда
		
		ФорматСообщения = Взаимодействия.ФорматСообщенияПоУмолчанию(Пользователи.ТекущийПользователь());
		
		// Если тип текста не был заполнен, то может оказаться, что формат выбран некорректно, поэтому:
		// 1) Если текст заполнен, но не заполнен HTML - то формат сообщения исправляем на "текст"
		// 2) Если HTML заполнен, но не заполнен текст - то формат сообщения исправляем на "HTML"
		Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст 
			И СокрЛП(Объект.Текст) = "" И СокрЛП(Объект.ТекстHTML) <> "" Тогда
			ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML;
		ИначеЕсли ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML
			И СокрЛП(Объект.Текст) <> "" И СокрЛП(Объект.ТекстHTML) = "" Тогда
			ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст;
		КонецЕсли;
		
	Иначе
		
		Если Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст Тогда
			ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст;
		Иначе
			ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("ОтправлятьПисьмаВФорматеHTML") 
			И ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
			ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст;
		КонецЕсли;
		
		ПараметрыРаботыПользователяПользователя =
			Взаимодействия.ПолучитьПараметрыРаботыПользователяДляИсходящегоЭлектронногоПисьма(
			Объект.УчетнаяЗапись,
			ФорматСообщения,
			?(Объект.ВзаимодействиеОснование = Неопределено, Истина, Ложь));
		
		Объект.УведомитьОДоставке            = ПараметрыРаботыПользователяПользователя.УведомитьОДоставке;
		Объект.УведомитьОПрочтении           = ПараметрыРаботыПользователяПользователя.УведомитьОПрочтении;
		Объект.ВключатьТелоИсходногоПисьма   = ПараметрыРаботыПользователяПользователя.ВключатьТелоИсходногоПисьма;
		Объект.ОтображатьТелоИсходногоПисьма = ПараметрыРаботыПользователяПользователя.ОтображатьТелоИсходногоПисьма;
		
	КонецЕсли;
	
	Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		//АТ+
		//Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаФорматированныйДокумент;
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаТекстHTML;
		//АТ-
		
		Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTMLСКартинками;
		Если Не Объект.Ссылка.Пустая() Тогда
			
			//АТ+
			СтруктураВложений = Новый Структура;
			
			//Объект.ТекстHTML = Взаимодействия.ОбработатьТекстHTMLДляФорматированногоДокумента(
			//	Объект.Ссылка, Объект.ТекстHTML, СтруктураВложений);
			//ТекстПисьмаФорматированныйДокумент.УстановитьHTML(Объект.ТекстHTML, СтруктураВложений);
			ФорматированныйТекстHTML = Взаимодействия.ОбработатьТекстHTMLДляФорматированногоДокумента(
				Объект.Ссылка, Объект.ТекстHTML, СтруктураВложений);
			ТекстПисьмаФорматированныйДокумент.УстановитьHTML(ФорматированныйТекстHTML, СтруктураВложений);
			//АТ-
			
		Иначе
			
			//АТ+
			Если Объект.ВключатьТелоИсходногоПисьма
				И Объект.ВзаимодействиеОснование <> Неопределено И НЕ Объект.ВзаимодействиеОснование.Пустая() Тогда
				
				Объект.ТекстHTML = СформироватьТекстПисьмаВключаяПисьмоОснование(
					//Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(Объект.ТекстHTML),
					Неопределено,
					Объект);
				
			КонецЕсли; 
			
			ВставитьПодпись();
			
			СтруктураВложений = Новый Структура;
			ТекстHTMLФД = Взаимодействия.ОбработатьТекстHTMLДляФорматированногоДокумента(
				Объект.Ссылка, Объект.ТекстHTML, СтруктураВложений);
			ТекстПисьмаФорматированныйДокумент.УстановитьHTML(ТекстHTMLФД, СтруктураВложений);
			//АТ-
			
		КонецЕсли;
		
	Иначе         
		
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаОбычныйТекст;
		Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
		Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст;
		
		//АТ+
		//ТекстПисьма = Объект.Текст;
		
		Если Объект.ВключатьТелоИсходногоПисьма
			И Объект.ВзаимодействиеОснование <> Неопределено И НЕ Объект.ВзаимодействиеОснование.Пустая() Тогда
			
			ТекстПисьма = ОбработчикиСобытийДляПочтовыхДокументов_ат.ПолучитьТексПисьмаИзРегистраСодержаниеЭлектронныхПисем(
				Объект.ВзаимодействиеОснование);
			
		КонецЕсли; 
		
		ВставитьПодпись();
		//АТ-
		
		Объект.Кодировка = "UTF-8";
		
	КонецЕсли;
	
	Элементы.ФорматСообщения.Видимость = Истина;
	Элементы.ФорматСообщения.Заголовок = ФорматСообщения;
	
КонецПроцедуры

//Обрабатывает переданные параметры при создании письма
&НаСервере
Процедура ОбработатьПереданныеПараметры(Параметры)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если Параметры.Свойство("Вложения") И Параметры.Вложения <> Неопределено Тогда
			
			Для каждого Вложение Из Параметры.Вложения Цикл
				
				НоваяСтрока = Вложения.Добавить();
				Если ЭтоАдресВременногоХранилища(Вложение.Значение) Тогда
					НоваяСтрока.Расположение = 4;
					Вложение.Значение = ПоместитьВоВременноеХранилище(ПолучитьИзВременногоХранилища(Вложение.Значение), УникальныйИдентификатор);
					Расширение = ВзаимодействияКлиентСервер.ПолучитьРасширениеФайла(Вложение.Представление);
				Иначе
					НоваяСтрока.Расположение = 2;
					ИмяФайла   = "";
					Расширение = "";
					ВзаимодействияКлиентСервер.ПолучитьКаталогИИмяФайла(Вложение.Значение, "", ИмяФайла);
					Расширение = ВзаимодействияКлиентСервер.ПолучитьРасширениеФайла(ИмяФайла);
				КонецЕсли;
				
				НоваяСтрока.ИмяФайлаНаКомпьютере = Вложение.Значение;
				НоваяСтрока.ИмяФайла             = Вложение.Представление;
				НоваяСтрока.ИндексКартинки       = ФайловыеФункцииСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(Расширение);
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Параметры.Тема) Тогда
			Объект.Тема = Параметры.Тема;
		КонецЕсли;
		
		Если ТипЗнч(Параметры.Тело) = Тип("Структура") Тогда
			ТекстПисьмаФорматированныйДокумент.УстановитьHTML(Параметры.Тело.ТекстHTML, Параметры.Тело.СтруктураВложений);
			Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTMLСКартинками;
		ИначеЕсли ТипЗнч(Параметры.Тело) = Тип("Строка") И Не ПустаяСтрока(Параметры.Тело) Тогда
			Объект.Текст = Параметры.Тело;
			Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст;
		КонецЕсли;
		
		Если Параметры.Свойство("Кому") И Параметры.Кому <> Неопределено Тогда
			
			Если ТипЗнч(Параметры.Кому) = Тип("Строка") И НЕ ПустаяСтрока(Параметры.Кому) Тогда
				Объект.СписокПолучателейПисьма = Параметры.Кому;
				НоваяСтрока = Объект.ПолучателиПисьма.Добавить();
				НоваяСтрока.Адрес = Параметры.Кому;
				
			ИначеЕсли ТипЗнч(Параметры.Кому) = Тип("СписокЗначений") Тогда
				
				Для Каждого ЭлементСписка Из Параметры.Кому Цикл
					НоваяСтрока = Объект.ПолучателиПисьма.Добавить();
					НоваяСтрока.Адрес = ЭлементСписка.Значение;
					НоваяСтрока.Представление = ЭлементСписка.Представление;
				КонецЦикла;
				
				Объект.СписокПолучателейПисьма = ВзаимодействияКлиентСервер.ПолучитьПредставлениеСпискаАдресатов(Объект.ПолучателиПисьма, Ложь);
				
			ИначеЕсли ТипЗнч(Параметры.Кому) = Тип("Массив") Тогда
				
				Для Каждого ЭлементМассива Из Параметры.Кому Цикл
					НоваяСтрока = Объект.ПолучателиПисьма.Добавить();
					НоваяСтрока.Адрес = ЭлементМассива.Адрес;
					НоваяСтрока.Представление = ЭлементМассива.Представление;
					НоваяСтрока.Контакт = ЭлементМассива.ИсточникКонтактнойИнформации;
				КонецЦикла;
				
				Объект.СписокПолучателейПисьма = ВзаимодействияКлиентСервер.ПолучитьПредставлениеСпискаАдресатов(Объект.ПолучателиПисьма, Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ Параметры.УчетнаяЗапись.Пустая() Тогда
			
			Объект.УчетнаяЗапись = Параметры.УчетнаяЗапись;
			РеквизитыОтправителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Параметры.УчетнаяЗапись,"Ссылка, ИмяПользователя, АдресЭлектроннойПочты");
			Объект.ОтправительПредставление = ВзаимодействияКлиентСервер.ПолучитьПредставлениеАдресата(
				РеквизитыОтправителя.ИмяПользователя, РеквизитыОтправителя.АдресЭлектроннойПочты, "");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//Определяет необходимость отображения письма основания
&НаСервере
Процедура ОтобразитьПисьмоОснование()
	
	Если Объект.ВзаимодействиеОснование <> Неопределено И НЕ Объект.ВзаимодействиеОснование.Пустая()
		И Объект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Черновик 
		И (ТипЗнч(Объект.ВзаимодействиеОснование) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее")
		ИЛИ ТипЗнч(Объект.ВзаимодействиеОснование) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее")) Тогда
		
		ЗначенияРеквизитовВходящегоПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.ВзаимодействиеОснование, "ТипТекста,ТекстHTML,Текст");
		
		ТипТекстаВходящегоПисьма = ?(ЗначенияРеквизитовВходящегоПисьма.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст,
			Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст,
			Перечисления.ТипыТекстовЭлектронныхПисем.HTML);
		
		Если ПолучитьФункциональнуюОпцию("ОтправлятьПисьмаВФорматеHTML") Тогда
			
			//ат+
			//Если ЗначенияРеквизитовВходящегоПисьма.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст Тогда
			//	
			//	ПростойТекстВходящегоПисьма = ..
			//	ТекстВходящегоПисьма = ПростойТекстВходящегоПисьма;
			//	Элементы.ТекстВходящегоПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
			//	
			//Иначе
			//	
			//	ТекстВходящегоПисьма = Взаимодействия.ОбработатьТекстHTML(Объект.ВзаимодействиеОснование); // меняем ID на ссылки во временном хранилище
			//	Элементы.ТекстВходящегоПисьма.Вид = ВидПоляФормы.ПолеHTMLДокумента;
			//	
			//КонецЕсли;
			//
			//Если Не Объект.ОтображатьТелоИсходногоПисьма Тогда
			//	Элементы.ГруппаВходящее.Видимость = Ложь;
			//Иначе
			//	Элементы.ОтображатьТекстПисьмаОснования.Пометка = Истина;
			//КонецЕсли;
			//ат-
			
		Иначе
			
			Элементы.ГруппаВходящее.Видимость = Ложь;
			ТекстПисьма = СформироватьТекстПисьмаВключаяПисьмоОснование(Неопределено, Объект);
			
		КонецЕсли;
		
	Иначе
		
		Элементы.ГруппаВходящее.Видимость = Ложь;
		Элементы.ОтображатьТекстПисьмаОснования.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВыполнить()
	
	ОчиститьСообщения();
	
	НайденныеСтроки = ДоступныеДляОтправкиУчетныеЗаписи.НайтиСтроки(Новый Структура("УчетнаяЗапись", Объект.УчетнаяЗапись));
	Если НайденныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru ='Выбранная учетная запись не доступна для отправки писем'"),, "ОтправительПредставление", "Объект");
		Возврат;
	Иначе
		
		Если НайденныеСтроки[0].УдалятьПослеОтправки Тогда
			СписокКнопок = Новый СписокЗначений;
			СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Отправить'"));
			СписокКнопок.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отправить и сохранить'"));
			СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));

			Результат = Вопрос(НСтр("ru = 'Для данной учетной записи не предусмотрено сохранение отправленных писем в системе.
			                              |Продолжить?'"),
			                   СписокКнопок,, КодВозвратаДиалога.Да,
			                   НСтр("ru = 'Отправка письма'"));
			
			Если Результат = КодВозвратаДиалога.Да Тогда
				Объект.УдалятьПослеОтправки = Истина;
			ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
				Объект.УдалятьПослеОтправки = Ложь;
			ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ОтправлятьСообщенияСразу Тогда
			ОтправлятьСообщенияСразу = (
				Объект.СтатусПисьма = ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Исходящее"));
		КонецЕсли;
		
	КонецЕсли;
	
	ВыполняетсяОтправка = Истина;
	
	Если Объект.Ссылка.Пустая() ИЛИ
		Модифицированность ИЛИ
		Объект.ВключатьТелоИсходногоПисьма ИЛИ
		(Объект.СтатусПисьма = ПредопределенноеЗначение("Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Черновик")) Тогда
		
		Записать();
	КонецЕсли;
	
	ВыполняетсяОтправка = Ложь;
	
	Если ОтправлятьСообщенияСразу Тогда
		Результат = ВыполнитьОтправкуНаСервере();
	Иначе
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если Результат = "" Тогда
		Закрыть();
	Иначе
		Предупреждение(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереслатьВыполнить()
	
	Основание = Новый Структура("Основание,Команда", Объект.Ссылка, "Переслать");
	ПараметрыОткрытия = Новый Структура("Основание", Основание);
	ОткрытьФорму("Документ.ЭлектронноеПисьмоИсходящее.Форма.ФормаДокумента", ПараметрыОткрытия);

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокКнопкиПоУмолчанию()
	
	Если Объект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Отправлено Тогда
		Элементы.Отправить.Заголовок = Нстр("ru = 'Переслать'");
	ИначеЕсли Объект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Исходящее Тогда
		Элементы.Отправить.Заголовок = Нстр("ru = 'Отправить сейчас'");
	ИначеЕсли Объект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Черновик Тогда
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			НастройкиРаботыСПочтой = Взаимодействия.ПолучитьНастройкуРаботаСПочтой();
			Если НастройкиРаботыСПочтой.Свойство("ОтправлятьСообщенияСразу") И НастройкиРаботыСПочтой.ОтправлятьСообщенияСразу Тогда
				ОтправлятьСообщенияСразу = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.Отправить.Заголовок = Нстр("ru = 'Отправить'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматСообщенияПриИзменении()
	
	Если Объект.ТипТекста <> ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст") И 
		ФорматСообщения = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст") Тогда
		
		Если Не ВзаимодействияКлиент.ПриИзмененииФорматаСообщенияНаОбычныйТекст() Тогда
			ФорматСообщения = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML");
			Возврат;
		КонецЕсли;
		
		ТекстПисьма = ТекстПисьмаФорматированныйДокумент.ПолучитьТекст();
		Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.ПростойТекст");
		ТекстПисьмаФорматированныйДокумент.Удалить();
		Элементы.ТекстПисьма.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
		Объект.ТекстHTML = "";
		Объект.Кодировка = "UTF-8";
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаОбычныйТекст;
		
	Иначе
		
		ТекстПисьмаФорматированныйДокумент.Добавить(ТекстПисьма);
		Объект.Текст = "";
		Объект.ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.HTML");
		
		//АТ+
		//Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаФорматированныйДокумент;
		Элементы.СтраницыТекстПисьма.ТекущаяСтраница = Элементы.СтраницаТекстHTML;
		//АТ-
		
	КонецЕсли;
	
	ВставитьПодпись(); //АТ
	
	Элементы.ФорматСообщения.Заголовок = ФорматСообщения;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииИПриЧтенииНаСервере()
	
	Взаимодействия.УстановитьЗаголовокФормыЭлектронногоПисьма(ЭтаФорма);
	УстановитьЗаголовокКнопкиПоУмолчанию();
	ОбработатьПереданныеПараметры(Параметры);
	ЗаполнитьВложения(Параметры);
	
	Для Каждого ПолучательПисьма Из Объект.ПолучателиПисьма Цикл
		Если ЗначениеЗаполнено(ПолучательПисьма.Контакт) Тогда
			СоответствияАдресовИКонтактов.Добавить(ПолучательПисьма.Контакт, ПолучательПисьма.Адрес);
		КонецЕсли;
	КонецЦикла;
	
	ОпределитьВидимостьДоступностьЭлементовВЗависимостиОтСтатусаПисьма();
	ОтобразитьПисьмоОснование();
	
	НаКонтроле = НЕ Объект.Рассмотрено;
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Предмет = Взаимодействия.ПолучитьЗначениеПредмета(Объект.Ссылка);
		ТекущийСтатусПисьма = Объект.СтатусПисьма;
	КонецЕсли;
	
	ВзаимодействияКлиентСервер.ПроверитьЗаполнениеКонтактов(Объект, ЭтаФорма, "ЭлектронноеПисьмоИсходящее");
	
	Если Объект.СтатусПисьма = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Отправлено Тогда
		Элементы.СписокПолучателейКопий.КнопкаВыбора        = Ложь;
		Элементы.СписокПолучателейПисьма.КнопкаВыбора       = Ложь;
		Элементы.СписокПолучателейСкрытыхКопий.КнопкаВыбора = Ложь;
		Элементы.Тема.РедактированиеТекста                  = Ложь;
	КонецЕсли;
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначения.ПолучитьКартинкуКомментария(Объект.Комментарий);
	
	//ат+
	Уведомления_ат.УстановитьРеквизитФормыНаКонтролеПисьма(Объект.Ссылка, НаКонтроле);
	//ат-
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция   ПолучитьДанныеВыбораДляЭлектронногоПисьма(Текст, ИндексВСпискеПолучателей)
	
	ВозвращаемыйСписок = Новый СписокЗначений;
	МассивОписанияТиповКонтактов = ВзаимодействияКлиентСервер.ПолучитьМассивОписанияВозможныхКонтактов();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка КАК Контакт,
	|	ПРЕДСТАВЛЕНИЕ(Таблица.Ссылка) КАК Представление,
	|	ТаблицаКонтактнаяИнформация.АдресЭП КАК АдресЭП 
	|ИЗ
	|	Справочник.Пользователи КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ТаблицаКонтактнаяИнформация
	|		ПО (ТаблицаКонтактнаяИнформация.Ссылка = Таблица.Ссылка)
	|			И (ТаблицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты) И (ТаблицаКонтактнаяИнформация.АдресЭП <> """"))
	|ГДЕ
	|	НЕ Таблица.ПометкаУдаления И //АТ
	|	(Таблица.Наименование ПОДОБНО &ВведеннаяСтрока
	|			ИЛИ ТаблицаКонтактнаяИнформация.АдресЭП ПОДОБНО &ВведеннаяСтрока
	|			ИЛИ ТаблицаКонтактнаяИнформация.ДоменноеИмяСервера ПОДОБНО &ВведеннаяСтрока
	|			ИЛИ ТаблицаКонтактнаяИнформация.Представление ПОДОБНО &ВведеннаяСтрока)";
	
	Для каждого ЭлементМассиваОписания Из МассивОписанияТиповКонтактов Цикл
		
		Если ЭлементМассиваОписания.Имя = "Пользователи" Тогда
			Продолжить;
		Иначе
			
			УсловиеПолейВводаПоСтроке = "";
			СписокПолей = Взаимодействия.ПолучитьСвойствоМетаданныхПоТипу(ЭлементМассиваОписания.Тип, "ВводПоСтроке");
			Для Каждого Поле Из СписокПолей Цикл
				УсловиеПолейВводаПоСтроке = УсловиеПолейВводаПоСтроке + " ИЛИ Таблица." + Поле.Имя + " ПОДОБНО &ВведеннаяСтрока";
			КонецЦикла;
			
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Таблица.Ссылка КАК Контакт,
			|	ПРЕДСТАВЛЕНИЕ(Таблица.Ссылка) КАК Представление,
			|	ТаблицаКонтактнаяИнформация.АдресЭП КАК АдресЭП
			|ИЗ
			|	Справочник." + ЭлементМассиваОписания.Имя + " КАК Таблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник." + ЭлементМассиваОписания.Имя + ".КонтактнаяИнформация КАК ТаблицаКонтактнаяИнформация
			|		ПО (ТаблицаКонтактнаяИнформация.Ссылка = Таблица.Ссылка)
			|			И (ТаблицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты) И (ТаблицаКонтактнаяИнформация.АдресЭП <> """"))
			|ГДЕ
			|	НЕ Таблица.ПометкаУдаления И //АТ
			|	(ТаблицаКонтактнаяИнформация.АдресЭП ПОДОБНО &ВведеннаяСтрока
			|			ИЛИ ТаблицаКонтактнаяИнформация.ДоменноеИмяСервера ПОДОБНО &ВведеннаяСтрока
			|			ИЛИ ТаблицаКонтактнаяИнформация.Представление ПОДОБНО &ВведеннаяСтрока 
			|" + УсловиеПолейВводаПоСтроке + ") 
			|";
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВведеннаяСтрока", Текст + "%");
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ВозвращаемыйСписок;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗначениеВыбора = Новый Структура;
		ЗначениеВыбора.Вставить("Контакт", Выборка.Контакт);
		ЗначениеВыбора.Вставить("Адрес", Выборка.АдресЭП);
		ЗначениеВыбора.Вставить("Представление", Выборка.Представление);
		ЗначениеВыбора.Вставить("ИндексВСпискеПолучателей", ИндексВСпискеПолучателей);
		ВозвращаемыйСписок.Добавить(ЗначениеВыбора, 
			ВзаимодействияКлиентСервер.ПолучитьПредставлениеАдресата(Выборка.Представление, Выборка.АдресЭП, ""));
		
	КонецЦикла;
	
	Возврат ВозвращаемыйСписок;
	
КонецФункции 

&НаКлиенте
Функция   ПолучитьИмяСвязаннойТаблицы(ИмяЭлемента)
	
	Если ИмяЭлемента = "СписокПолучателейПисьма" Тогда
		Возврат "ПолучателиПисьма";
	ИначеЕсли ИмяЭлемента = "СписокПолучателейКопий" Тогда
		Возврат "ПолучателиКопий";
	ИначеЕсли ИмяЭлемента = "СписокПолучателейСкрытыхКопий" Тогда
		Возврат "ПолучателиСкрытыхКопий";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция   ПолучитьКонтактПоАдресу(Адрес)
	
	Для Каждого Элемент Из СоответствияАдресовИКонтактов Цикл
		Если Элемент.Представление = Адрес Тогда
			Возврат Элемент.Значение;
		КонецЕсли;
	КонецЦикла;

	Контакт = Адрес;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъектСКонтактнойИнформацией.Ссылка
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ОбъектСКонтактнойИнформацией
	|ГДЕ
	|	ОбъектСКонтактнойИнформацией.Ссылка.ПометкаУдаления И //АТ
	|	ОбъектСКонтактнойИнформацией.АдресЭП = &АдресЭП
	|	И ОбъектСКонтактнойИнформацией.Тип = &Тип
	|	И (НЕ ОбъектСКонтактнойИнформацией.Ссылка.ПометкаУдаления)";
	
	МассивОписанияТиповКонтактов = ВзаимодействияКлиентСервер.ПолучитьМассивОписанияВозможныхКонтактов();
	Для каждого ЭлементМассиваОписания Из МассивОписанияТиповКонтактов Цикл
		
		Если ЭлементМассиваОписания.Имя = "Пользователи" Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст + "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОбъектСКонтактнойИнформацией.Ссылка
		|ИЗ
		|	Справочник." + ЭлементМассиваОписания.Имя + ".КонтактнаяИнформация КАК ОбъектСКонтактнойИнформацией
		|ГДЕ
		|	ОбъектСКонтактнойИнформацией.Ссылка.ПометкаУдаления И //АТ
		|	ОбъектСКонтактнойИнформацией.АдресЭП = &АдресЭП
		|	И ОбъектСКонтактнойИнформацией.Тип = &Тип
		|	И (НЕ ОбъектСКонтактнойИнформацией.Ссылка.ПометкаУдаления)
		|";		
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("АдресЭП", Адрес);
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Контакт = Выборка.Ссылка;
	КонецЕсли;
	
	СоответствияАдресовИКонтактов.Добавить(Контакт, Адрес);
	Возврат Контакт;
	
КонецФункции

&НаКлиенте
Функция   ПолучитьПредставлениеИдентификаторовОснований(Знач Идентификаторы)

	Идентификаторы = СтрЗаменить(Идентификаторы, "<",  " ");
	Идентификаторы = СтрЗаменить(Идентификаторы, ">",  " ");
	Идентификаторы = СтрЗаменить(Идентификаторы, "  ", " ");
	Идентификаторы = СокрЛП(СтрЗаменить(Идентификаторы, "  ", " "));
	Идентификаторы = СтрЗаменить(Идентификаторы, " ", Символы.ПС + "                          ");
	
	Возврат Идентификаторы;

КонецФункции

&НаСервере
Процедура ПреобразоватьИменаПолучателей(СтрокаИмен, ИмяТаблицы, ИмяЭлемента)
	
	ТаблицаПолучатели = Объект[ИмяТаблицы];
	
	ТаблицаПолучатели.Очистить();
	Если ПустаяСтрока(СтрокаИмен) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Результат = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(СтрокаИмен);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),,ИмяЭлемента,"Объект");
		Возврат;
	КонецПопытки;
	
	Если Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из Результат Цикл
		НоваяСтрока = ТаблицаПолучатели.Добавить();
		НоваяСтрока.Адрес = ЭлементМассива.Адрес;
		НоваяСтрока.Представление = ЭлементМассива.Представление;
		НоваяСтрока.Контакт = ?(ПустаяСтрока(ЭлементМассива.Адрес), "", ПолучитьКонтактПоАдресу(ЭлементМассива.Адрес));
	КонецЦикла;
	
	Модифицированность = Истина;
	
	ВзаимодействияКлиентСервер.ПроверитьЗаполнениеКонтактов(Объект,ЭтаФорма,"ЭлектронноеПисьмоИсходящее");
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСписокПолучателей(ДляПодбора, ГруппаПодбора = "")
	
	// Получим список адресатов
	СоответствиеТабличныхЧастей = Новый Соответствие;
	СоответствиеТабличныхЧастей.Вставить("Кому", Объект.ПолучателиПисьма);
	СоответствиеТабличныхЧастей.Вставить("Копии", Объект.ПолучателиКопий);
	СоответствиеТабличныхЧастей.Вставить("Скрытые", Объект.ПолучателиСкрытыхКопий);
	СоответствиеТабличныхЧастей.Вставить("Ответ", Объект.ПолучателиОтвета);
	
	СписокВыбранных = Новый СписокЗначений;
	Для Каждого ТабличнаяЧасть Из СоответствиеТабличныхЧастей Цикл
		СписокВыбранных.Добавить(
			УправлениеЭлектроннойПочтойКлиент.ТаблицуКонтактовВМассив(ТабличнаяЧасть.Значение), ТабличнаяЧасть.Ключ);
	КонецЦикла;

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("УчетнаяЗапись", Объект.УчетнаяЗапись);
	ПараметрыОткрытия.Вставить("СписокВыбранных", СписокВыбранных);
	ПараметрыОткрытия.Вставить("Предмет", Предмет);
	ПараметрыОткрытия.Вставить("Письмо", Объект.Ссылка);
	ПараметрыОткрытия.Вставить("ГруппаПоУмолчанию",?(ПустаяСтрока(ГруппаПодбора),НСтр("ru = 'Кому'"), ГруппаПодбора));
	
	// Откроем форму для редактирования списка адресатов
	ИмяОбщейФормы = ?(ДляПодбора, "ОбщаяФорма.АдреснаяКнига", "ОбщаяФорма.УточнениеКонтактов");
	
	ОткрытьФорму(ИмяОбщейФормы, ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодобранныхПолучателей(СоответствиеТабличныхЧастей, Результат)

	Для Каждого ТабличнаяЧасть Из СоответствиеТабличныхЧастей Цикл
		ТабличнаяЧасть.Значение.Очистить();
	КонецЦикла;
	
	Для Каждого Элемент Из Результат Цикл
		
		ТабличнаяЧасть = СоответствиеТабличныхЧастей.Получить(Элемент.Группа);
		Если ТабличнаяЧасть = Неопределено Тогда
			ТабличнаяЧасть = Объект.ПолучателиПисьма;
		КонецЕсли;
		
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		НоваяСтрока.Адрес = Элемент.Адрес;
		НоваяСтрока.Представление = Элемент.Представление;
		НоваяСтрока.Контакт = Элемент.Контакт;
		
	КонецЦикла;
	
	СформироватьСпискиПолучателей();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСпискиПолучателей()

	Объект.СписокПолучателейПисьма =
		ВзаимодействияКлиентСервер.ПолучитьПредставлениеСпискаАдресатов(Объект.ПолучателиПисьма, Ложь);
	Объект.СписокПолучателейКопий =
		ВзаимодействияКлиентСервер.ПолучитьПредставлениеСпискаАдресатов(Объект.ПолучателиКопий, Ложь);
	Объект.СписокПолучателейСкрытыхКопий = 
		ВзаимодействияКлиентСервер.ПолучитьПредставлениеСпискаАдресатов(Объект.ПолучателиСкрытыхКопий, Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУточненныеКонтакты(Результат)
	
	Объект.ПолучателиКопий.Очистить();
	Объект.ПолучателиОтвета.Очистить();
	Объект.ПолучателиПисьма.Очистить();
	Объект.ПолучателиСкрытыхКопий.Очистить();
	
	Для каждого ЭлементМассива Из Результат Цикл
	
		Если ЭлементМассива.Группа = "Кому" Тогда
			ТаблицаПолучателей = Объект.ПолучателиПисьма;
		ИначеЕсли ЭлементМассива.Группа = "Копии" Тогда
			ТаблицаПолучателей = Объект.ПолучателиКопий;
		ИначеЕсли ЭлементМассива.Группа = "Скрытые" Тогда
			ТаблицаПолучателей = Объект.ПолучателиСкрытыхКопий;
		Иначе
			ТаблицаПолучателей = Объект.ПолучателиОтвета;
		КонецЕсли;
		
		СтрокаПолучатели = ТаблицаПолучателей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПолучатели, ЭлементМассива);
	
	КонецЦикла;
	
	СформироватьСпискиПолучателей();

КонецПроцедуры

&НаСервере
Функция   ВыполнитьОтправкуНаСервере()
	
	ПисьмоОбъект = РеквизитФормыВЗначение("Объект");
	
	Попытка
		ИдентификаторПисьма = Взаимодействия.ВыполнитьОтправкуПисьма(ПисьмоОбъект);
	Исключение
		ЗначениеВРеквизитФормы(ПисьмоОбъект, "Объект");
		Возврат КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Если НЕ ПисьмоОбъект.УдалятьПослеОтправки Тогда
		
		ПисьмоОбъект.ИдентификаторСообщения = ИдентификаторПисьма;
		ПисьмоОбъект.СтатусПисьма    = Перечисления.СтатусыИсходящегоЭлектронногоПисьма.Отправлено;
		ПисьмоОбъект.ДатаОтправления = ТекущаяДатаСеанса();
		ПисьмоОбъект.Записать(РежимЗаписиДокумента.Запись);
		ЗначениеВРеквизитФормы(ПисьмоОбъект, "Объект");
		
		ВзаимодействияВызовСервера.УстановитьПапкуЭлектронногоПисьма(
			Объект.Ссылка, Взаимодействия.ОпределитьПапкуДляПисьма(Объект.Ссылка));
		ТекущийСтатусПисьма = Объект.СтатусПисьма;
		
	Иначе
		
		ПисьмоОбъект.Прочитать();
		ПисьмоОбъект.Удалить();
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаСервере
Функция   ОценитьРазмерПисьма()

	Размер = СтрДлина(Объект.Тема)*2;
	Размер = Размер + 
		?(Объект.ТипТекста = Перечисления.ТипыТекстовЭлектронныхПисем.HTML,
		СтрДлина(Объект.ТекстHTML),
		СтрДлина(Объект.Текст))*2;
	
	Для каждого Вложение Из Вложения Цикл
		Размер = Размер + Вложение.Размер * 1.5;
	КонецЦикла;
	
	Для каждого СтрокаТаблицыСоответствий Из ТаблицаСоответствийИменВложенийИдентификаторам Цикл
		Размер = Размер + СтрокаТаблицыСоответствий.Картинка.ПолучитьДвоичныеДанные().Размер()*1.5;
	КонецЦикла;
	
	Возврат Размер;

КонецФункции

&НаСервере
Функция   РеквизитыФайла(Файл)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Файл, "Наименование, Расширение, ИндексКартинки, Размер, ПодписанЭЦП ");
	
КонецФункции

// СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////

//АТ+

&НаКлиенте
Процедура УправлениеДоступностьюРассмотретьПосле()
	
	Элементы.РассмотретьПосле.Доступность = НаКонтроле;
	
КонецПроцедуры

&НаСервере
Процедура ВставитьПодпись()
	
	УЗ = Объект.УчетнаяЗапись;
	
	Если ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		ПодписьПользователя = ПолучитьПодпись(УЗ, Ложь);
		
		Если ПодписьПользователя <> Неопределено Тогда
			
			//РаботаСHTML_КлиентСервер_ат.ОбъединитьДокументыHTML(
			HTMLТекстПодписи = "";
			ВложенияПодписи = Новый Структура();
			ПодписьПользователя.ПолучитьHTML(HTMLТекстПодписи, ВложенияПодписи);
			
			Если НЕ ПустаяСтрока(Объект.ТекстHTML) Тогда
				
				ДокументHTML = Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(Объект.ТекстHTML);
				ЭлементТелоПисьма = ДокументHTML.Тело;
				
				МассивДочернихУзловТела = Взаимодействия.ПолучитьМассивДочернихУзловСодержащихHTML(ЭлементТелоПисьма);
				ЭлементПодписи = Взаимодействия.ДобавитьЭлементСАтрибутами(
					ЭлементТелоПисьма, "div", Новый Структура("id", "ATsign"));
				РаботаСHTML_Сервер_ат.ИмпортироватьДокументВЭлемент(ДокументHTML, ЭлементПодписи,
					Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(HTMLТекстПодписи));
				Взаимодействия.ВставитьЭлементHTMLПервымДочернимЭлементом(ЭлементТелоПисьма, ЭлементПодписи, МассивДочернихУзловТела);
				Объект.ТекстHTML = Взаимодействия.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
				
			Иначе
				Объект.ТекстHTML = HTMLТекстПодписи;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст Тогда
		
		ПодписьПользователя = ПолучитьПодпись(УЗ, Истина);
		
		Если ПодписьПользователя <> Неопределено Тогда
			
			ТекстПисьма = Символы.ПС //ТекстПисьма - пока так, в связи с тем, что не работает удаление
				+ Символы.ПС + Символы.ПС + ПодписьПользователя;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция   ПолучитьПодпись(УЗ, ЭтоОбычныйТекст)
	
	Подпись = ПодписиЭлектронныхПисем_ат.ПолучитьПодписьПользователяПоУчетнойЗаписи(Пользователи.ТекущийПользователь(), УЗ);
	
	Если Подпись = Неопределено Тогда 
		Возврат Неопределено
	Иначе
		Если ЭтоОбычныйТекст Тогда 
			Возврат Подпись.Подпись;
		Иначе 
			Возврат Подпись.ПодписьHTML.Получить();
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УдалитьПодпись()
	
	ТекстПисьмаФорматированныйДокумент = УдалитьПодписьИзТекстПисьма(ТекстПисьмаФорматированныйДокумент);
	
КонецПроцедуры

// !!! ВООБЩЕ НЕ РАБОТАЕТ
&НаСервере
Функция   УдалитьПодписьИзТекстПисьма(ПараграфыТекстПисьма)
	
	Если ЭтотОбъект.ФорматСообщения = Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст Тогда
		
		ТекстПодписиОбычныйТекст = ПолучитьПодпись(Объект.УчетнаяЗапись, Истина);
	    ТекстПисьма = СтрЗаменить(ТекстПисьма, ТекстПодписиОбычныйТекст, "");
		
		//Сообщить("Данная функция доступна только для писем в HTML формате.");
		Возврат ПараграфыТекстПисьма;
		
	Иначе
		
		ТекстПисьмаHTML ="";
		ВложенияТелаПисьма = Новый Структура;
		ПараграфыТекстПисьма.ПолучитьHTML(ТекстПисьмаHTML, ВложенияТелаПисьма);
		
		НачальнаяПозицияТекстаПодписиВHTML = Найти(ТекстПисьмаHTML, "<p>Начало подписи</p>");
		КонечнаяПозицияТекстаПодписиВHTML = Найти(ТекстПисьмаHTML,  "<p>Конец подписи</p>") + 20; //28
		
		ТелоПодписи = Сред(ТекстПисьмаHTML,НачальнаяПозицияТекстаПодписиВHTML,
			КонечнаяПозицияТекстаПодписиВHTML - НачальнаяПозицияТекстаПодписиВHTML);
		//ТекстПодписиHTMLФорматированнныйДокумент = ПолучитьПодпись("", Ложь);
		//ТекстПодписиHTML="";
		//СтруктураВложений = Новый Структура;
		//ТекстПодписиHTMLФорматированнныйДокумент.ПолучитьHTML(ТекстПодписиHTML, СтруктураВложений);
		//НачальнаяПозицияТекстаПодписиВHTML = Найти(ТекстПодписиHTML,"<body>")+6;
		//КонечнаяПозицияТекстаПодписиВHTML = Найти(ТекстПодписиHTML,"</body>");
		//ТелоПодписи = сред(ТекстПодписиHTML, НачальнаяПозицияТекстаПодписиВHTML,
		//					КонечнаяПозицияТекстаПодписиВHTML-НачальнаяПозицияТекстаПодписиВHTML);
		ТелоПисьмаБезПодписи = СтрЗаменить(ТекстПисьмаHTML, ТелоПодписи, "");
		Если НЕ(НачальнаяПозицияТекстаПодписиВHTML = 0 Или КонечнаяПозицияТекстаПодписиВHTML = 20) Тогда
			ПараграфыТекстПисьма.УстановитьHTML(ТелоПисьмаБезПодписи, ВложенияТелаПисьма);
		КонецЕсли;
		
		Возврат ПараграфыТекстПисьма;
		
	КонецЕсли;
	
КонецФункции

#Область Метки

&НаКлиенте
Процедура ОтключитьОтборПоПроекту(Команда)
	
	//Элементы.ФормаИспользоватьОтборПоПроектам.Пометка = НЕ Элементы.ФормаИспользоватьОтборПоПроектам.Пометка;
	
	ОтключитьОтборПоПроектуНаСервере();
	Метки_Клиент_ат.ОбновитьПринадлежностьВыбранныхМетокКПроектам(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьОтборПоПроектуНаСервере()
	
	Элементы.ФормаИспользоватьОтборПоПроектам.Пометка = НЕ Элементы.ФормаИспользоватьОтборПоПроектам.Пометка;
	Метки_Сервер_ат.ОбновитьМетки(ЭтаФорма, Объект.Ссылка, Ложь, Элементы.ФормаИспользоватьОтборПоПроектам.Пометка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокМеток(Команда)
		
	ОткрытьФорму("Справочник.Метки_ат.ФормаСписка",, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМетокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Метки_Клиент_ат.УстановитьМетку(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеМеткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Метки_Клиент_ат.УстановитьМетку(ЭтаФорма, Истина,, Истина);
	
	ОбновитьКоличествоВыбранныхМеток();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМетокИспользоватьПриИзменении(Элемент)
	
	Метки_Клиент_ат.УстановитьМетку(ЭтаФорма, Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеМеткиПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ВыбранныеМетки.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Строка = Метки_Клиент_ат.ПолучитьИдентификаторСтроки(Элементы.ВыбранныеМетки.ТекущиеДанные.Значение, ДеревоМеток.ПолучитьЭлементы());
	
	Если НЕ Строка = Неопределено Тогда
		Элементы.ДеревоМеток.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	_ВыбранныеМетки = ВыбранныеМетки;
	
	Для Каждого СтрокаВыбранныхМеток Из _ВыбранныеМетки Цикл
		
		СтрокаВыбранныхМеток.Использовать = 0;
		Метки_Клиент_ат.ВыполнитьВыбор(ЭтаФорма, СтрокаВыбранныхМеток.Значение, СтрокаВыбранныхМеток.Использовать, СтрокаВыбранныхМеток.Частичная, СтрокаВыбранныхМеток.Сохраненная);
		
	КонецЦикла;
	
	ОбновитьКоличествоВыбранныхМеток();
	
КонецПроцедуры

&НаКлиенте
Процедура Восстановить(Команда)
	
	ВосстановитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНаСервере()
	
	Метки_Сервер_ат.ОбновитьМетки(ЭтаФорма, Объект.Ссылка,, Элементы.ФормаИспользоватьОтборПоПроектам.Пометка);
	ОбновитьКоличествоВыбранныхМеток();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоВыбранныхМеток()
	
	КоличествоВыбранныхМеток = 0;
	
	Для Каждого ВыбраннаяМетка Из ВыбранныеМетки Цикл
		
		Если ВыбраннаяМетка.Использовать > 0 Тогда
			КоличествоВыбранныхМеток = КоличествоВыбранныхМеток + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ГруппаМетки.Заголовок = "Метки" + ?(КоличествоВыбранныхМеток = 0, "", " (" + КоличествоВыбранныхМеток + ")");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьМетки()
	
	Метки_Сервер_ат.ОбновитьМетки(ЭтаФорма, Объект.Ссылка, Ложь, Элементы.ФормаИспользоватьОтборПоПроектам.Пометка);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ТекстHTMLДокументСформирован(Элемент)
	
	//ДобавитьОбработчик Элемент.Документ.body.OnContextMenu, ОбработчикСобытийТекстHTML;
	//ДобавитьОбработчик Элемент.Документ.body.OnPaste, ОбработчикСобытийТекстHTML;
	
	РаботаСHTML_Клиент_ат.ВключитьВозможностьРедактирования(ЭтаФорма,
		Элементы.ТекстHTML, Элементы.КоманднаяПанельТекстHTML,
		НЕ Объект.Ссылка.Пустая());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикСобытийТекстHTML(Событие)
	
	Если Событие.type = "contextmenu" Тогда
		
		ПолеМожноРедактировать = РаботаСHTML_Клиент_ат.HTMLПолеМожноРедактировать(Элементы.ТекстHTML);
		
		Для Каждого ЭлементКонтекстногоМеню Из Элементы.ТекстHTML.КонтекстноеМеню.ПодчиненныеЭлементы Цикл
			
			Если НЕ ЭлементКонтекстногоМеню.Имя = "ТекстHTML_КонтекстноеМеню_ВключитьВозможностьРедактирования"
				И НЕ ЭлементКонтекстногоМеню.Имя = "ТекстHTML_КонтекстноеМеню_ПроверитьОрфографию"
				И НЕ ЭлементКонтекстногоМеню.Имя = "ТекстHTML_КонтекстноеМеню_ПоказатьИсходныйКод" Тогда
				
				ЭлементКонтекстногоМеню.Доступность = ПолеМожноРедактировать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПолеМожноРедактировать Тогда
			
			Если Событие.srcElement.id = "red_marker" Тогда
				
				РаботаСHTML_Клиент_ат.ОбработатьВызовКонтекстногоМеню(Событие, КоординатыЗаменяемогоСлова, СоответствиеКомандЗаменыСловам);
				ИзменитьКонтестноеМенюЗаменыСловТекстHTML(СоответствиеКомандЗаменыСловам);
				
			Иначе
				
				ИзменитьКонтестноеМенюЗаменыСловТекстHTML(Неопределено, Истина);
				
			КонецЕсли;
			
		Иначе
			
			ИзменитьКонтестноеМенюЗаменыСловТекстHTML(Неопределено, Истина);
			
		КонецЕсли;
		
	ИначеЕсли Событие.type = "paste" Тогда
		
		ИдентификаторыКартинок.ЗагрузитьЗначения(РаботаСHTML_Клиент_ат.ПолучитьИдентификаторыКартинок(Элементы.ТекстHTML.Документ));
		ПодключитьОбработчикОжидания("УдалитьКартинкиВставленныеКопированием", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКартинкиВставленныеКопированием() Экспорт
	
	РаботаСHTML_Клиент_ат.УдалитьКартинкиВставленныеКопированием(Элементы.ТекстHTML.Документ,
		ИдентификаторыКартинок.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКонтестноеМенюЗаменыСловТекстHTML(СоответствиеКомандЗаменыСловам, ТолькоОчистить = Ложь)
	
	РаботаСHTML_Сервер_ат.ИзменитьКонтестноеМенюЗаменыСловПоляHTML(ЭтаФорма,
		Элементы.ТекстHTML.КонтекстноеМеню,
		СоответствиеКомандЗаменыСловам, ТолькоОчистить, "ОбработчикКомандТекстHTML");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикКомандТекстHTML(Команда, ВыбранноеЗначение)
	
	РаботаСHTML_Клиент_ат.ОбработчикКомандРаботыСHTML(ЭтаФорма, Команда, ВыбранноеЗначение,
		Элементы.ТекстHTML, Элементы.КоманднаяПанельТекстHTML,
		КоординатыЗаменяемогоСлова, СоответствиеКомандЗаменыСловам);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	РаботаСHTML_Клиент_ат.ИзменитьПометкиКнопок(Элементы.КоманднаяПанельТекстHTML,
		Элементы.ТекстHTML.Документ);
	
КонецПроцедуры

//АТ-
