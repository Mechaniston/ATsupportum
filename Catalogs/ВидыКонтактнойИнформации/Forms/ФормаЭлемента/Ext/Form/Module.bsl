
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбъектНаСервере = РеквизитФормыВЗначение("Объект");
	ЭтоПредопределенный = ОбъектНаСервере.Предопределенный;
	
	ИспользуетсяЗапретРедактированияРеквизитов = Ложь;
	УправлениеКонтактнойИнформацией.ПриОпределенииИспользованияЗапретаРедактированияРеквизитов(ИспользуетсяЗапретРедактированияРеквизитов);
	
	Если Не ЭтоПредопределенный Тогда
		// Обработчик подсистемы запрета редактирования реквизитов объектов.
		УправлениеКонтактнойИнформацией.ПриБлокировкеРеквизитовОбъекта(ЭтаФорма);
	КонецЕсли;
	
	Если ОбъектНаСервере.ЭтоНовый() Тогда
		
		Объект.МожноИзменятьСпособРедактирования = Истина;
		Объект.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес;
	Иначе
		
		Если ЭтоПредопределенный Тогда
			
			Элементы.Наименование.ТолькоПросмотр = Истина;
			Элементы.Родитель.ТолькоПросмотр = Истина;
			Элементы.Тип.ТолькоПросмотр = Истина;
			Элементы.Подсказка.ТолькоПросмотр = Истина;
			
		Иначе
			
			Если Не ИспользуетсяЗапретРедактированияРеквизитов Тогда
				
				Элементы.Родитель.ТолькоПросмотр = Истина;
				Элементы.Тип.ТолькоПросмотр = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.РазрешитьВводНесколькихЗначений.Доступность = ?(ОбъектНаСервере.Уровень() = 2, Ложь, Истина);
	
	Если Не Объект.МожноИзменятьСпособРедактирования Тогда
		
		Элементы.РедактированиеТолькоВДиалоге.Доступность = Ложь;
		Элементы.РазрешитьВводНесколькихЗначений.Доступность = Ложь;
		Элементы.ГруппаНаименованиеНастройкиПоТипам.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриИзмененииТип();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ТекущийОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ЭтоПредопределенный Тогда
		
		// Обработчик подсистемы запрета редактирования реквизитов объектов.
		УправлениеКонтактнойИнформацией.ПриБлокировкеРеквизитовОбъекта(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Очистить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	
	ПриИзмененииТип();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресТолькоРоссийскийПриИзменении(Элемент)
	
	ПриИзмененииАдресТолькоРоссийский();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьПоКлассификаторуПриИзменении(Элемент)
	
	 ПриИзмененииПроверятьКорректность();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не ЭтоПредопределенный Тогда
		
		РедактированиеРазрешено = Ложь;
		УправлениеКонтактнойИнформациейКлиент.ПриРазрешенииРедактированияРеквизитовОбъекта(ЭтаФорма, РедактированиеРазрешено);
		
		Если РедактированиеРазрешено Тогда
			
			УправлениеКонтактнойИнформациейКлиент.ПриУстановкеДоступностиЭлементовФормы(ЭтаФорма);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПриИзмененииАдресТолькоРоссийский()
	
	Элементы.ПроверятьПоКлассификатору.Доступность = Объект.АдресТолькоРоссийский;
	Элементы.СкрыватьНеактуальныеАдреса.Доступность = Объект.АдресТолькоРоссийский;
	
	Если Не Объект.АдресТолькоРоссийский Тогда
		
		Объект.ПроверятьКорректность = Ложь;
		Объект.СкрыватьНеактуальныеАдреса = Ложь;
		
	КонецЕсли;
	
	ПриИзмененииПроверятьКорректность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТип()
	
	Если Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес") Тогда
		Элементы.Проверки.ТекущаяСтраница = Элементы.Проверки.ПодчиненныеЭлементы.Адрес;
		Элементы.РедактированиеТолькоВДиалоге.Доступность = Объект.МожноИзменятьСпособРедактирования;
		ПриИзмененииАдресТолькоРоссийский();
	ИначеЕсли Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты") Тогда
		Элементы.Проверки.ТекущаяСтраница = Элементы.Проверки.ПодчиненныеЭлементы.АдресЭлектроннойПочты;
		Элементы.РедактированиеТолькоВДиалоге.Доступность = Ложь;
		Объект.РедактированиеТолькоВДиалоге = Ложь;
		ПриИзмененииПроверятьКорректность();
	ИначеЕсли Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон")
		Или Объект.Тип = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Факс") Тогда
		Элементы.Проверки.ТекущаяСтраница = Элементы.Проверки.ПодчиненныеЭлементы.Другое;
		Элементы.РедактированиеТолькоВДиалоге.Доступность = Объект.МожноИзменятьСпособРедактирования;
	Иначе
		Элементы.Проверки.ТекущаяСтраница = Элементы.Проверки.ПодчиненныеЭлементы.Другое;
		Элементы.РедактированиеТолькоВДиалоге.Доступность = Ложь;
		Объект.РедактированиеТолькоВДиалоге = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьКорректностьПриИзменении(Элемент)
	
	ПриИзмененииПроверятьКорректность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПроверятьКорректность()
	
	Объект.ЗапрещатьВводНекорректного = Объект.ПроверятьКорректность;
	
КонецПроцедуры


