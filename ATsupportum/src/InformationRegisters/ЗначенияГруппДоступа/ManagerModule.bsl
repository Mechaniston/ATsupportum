#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура обновляет данные регистра при изменении
// - разрешенных значений групп доступа,
// - разрешенных значений профилей групп доступа.
// 
// Параметры:
//  ГруппыДоступа - СправочникСсылка.ГруппыДоступа.
//                - Массив значений указанных выше типов.
//                - Неопределено - без отбора.
//
//  ЕстьИзменения - Булево (возвращаемое значение) - если производилась запись,
//                  устанавливается Истина, иначе не изменяется.
//
Процедура ОбновитьДанныеРегистра(ГруппыДоступа = Неопределено,
                                 ЕстьИзменения = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапросовВременныхТаблиц =
	"ВЫБРАТЬ
	|	ГруппыДоступа.Ссылка КАК ГруппаДоступа,
	|	ВидыДоступаПрофиля.ВидДоступа,
	|	ИСТИНА КАК ТолькоВидДоступа,
	|	НЕОПРЕДЕЛЕНО КАК ЗначениеДоступа
	|ПОМЕСТИТЬ НовыеДанные
	|ИЗ
	|	Справочник.ГруппыДоступа КАК ГруппыДоступа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.ВидыДоступа КАК ВидыДоступаПрофиля
	|		ПО ГруппыДоступа.Профиль = ВидыДоступаПрофиля.Ссылка
	|			И (ВидыДоступаПрофиля.Предустановленный)
	|			И (НЕ ВидыДоступаПрофиля.ДоступРазрешен)
	|			И (НЕ ГруппыДоступа.ПометкаУдаления)
	|			И (НЕ ВидыДоступаПрофиля.Ссылка.ПометкаУдаления)
	|			И (&УсловиеОтбораГруппДоступа1)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВидыДоступа.Ссылка,
	|	ВидыДоступа.ВидДоступа,
	|	ИСТИНА,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Справочник.ГруппыДоступа.ВидыДоступа КАК ВидыДоступа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.ВидыДоступа КАК ПредустановленныеВидыДоступа
	|		ПО ВидыДоступа.Ссылка.Профиль = ПредустановленныеВидыДоступа.Ссылка
	|			И ВидыДоступа.ВидДоступа = ПредустановленныеВидыДоступа.ВидДоступа
	|			И (НЕ ПредустановленныеВидыДоступа.Предустановленный)
	|			И (НЕ ВидыДоступа.ДоступРазрешен)
	|			И (НЕ ВидыДоступа.Ссылка.ПометкаУдаления)
	|			И (НЕ ПредустановленныеВидыДоступа.Ссылка.ПометкаУдаления)
	|			И (&УсловиеОтбораГруппДоступа2)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ГруппыДоступа.Ссылка,
	|	ЗначенияДоступаПрофиля.ВидДоступа,
	|	ЛОЖЬ,
	|	ЗначенияДоступаПрофиля.ЗначениеДоступа
	|ИЗ
	|	Справочник.ГруппыДоступа КАК ГруппыДоступа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.ВидыДоступа КАК ВидыДоступаПрофиля
	|		ПО ГруппыДоступа.Профиль = ВидыДоступаПрофиля.Ссылка
	|			И (ВидыДоступаПрофиля.Предустановленный)
	|			И (НЕ ГруппыДоступа.ПометкаУдаления)
	|			И (НЕ ВидыДоступаПрофиля.Ссылка.ПометкаУдаления)
	|			И (&УсловиеОтбораГруппДоступа1)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.ЗначенияДоступа КАК ЗначенияДоступаПрофиля
	|		ПО (ЗначенияДоступаПрофиля.Ссылка = ВидыДоступаПрофиля.Ссылка)
	|			И (ЗначенияДоступаПрофиля.ВидДоступа = ВидыДоступаПрофиля.ВидДоступа)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВидыДоступа.Ссылка,
	|	ЗначенияДоступа.ВидДоступа,
	|	ЛОЖЬ,
	|	ЗначенияДоступа.ЗначениеДоступа
	|ИЗ
	|	Справочник.ГруппыДоступа.ВидыДоступа КАК ВидыДоступа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиГруппДоступа.ВидыДоступа КАК ПредустановленныеВидыДоступа
	|		ПО ВидыДоступа.Ссылка.Профиль = ПредустановленныеВидыДоступа.Ссылка
	|			И ВидыДоступа.ВидДоступа = ПредустановленныеВидыДоступа.ВидДоступа
	|			И (НЕ ПредустановленныеВидыДоступа.Предустановленный)
	|			И (НЕ ВидыДоступа.Ссылка.ПометкаУдаления)
	|			И (НЕ ПредустановленныеВидыДоступа.Ссылка.ПометкаУдаления)
	|			И (&УсловиеОтбораГруппДоступа2)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа.ЗначенияДоступа КАК ЗначенияДоступа
	|		ПО (ЗначенияДоступа.Ссылка = ВидыДоступа.Ссылка)
	|			И (ЗначенияДоступа.ВидДоступа = ВидыДоступа.ВидДоступа)";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НовыеДанные.ГруппаДоступа,
	|	НовыеДанные.ВидДоступа,
	|	НовыеДанные.ТолькоВидДоступа,
	|	НовыеДанные.ЗначениеДоступа,
	|	&ПодстановкаПоляВидИзмененияСтроки
	|ИЗ
	|	НовыеДанные КАК НовыеДанные";
	
	// Подготовка выбираемых полей с необязательным отбором.
	Поля = Новый Массив; 
	Поля.Добавить(Новый Структура("ГруппаДоступа", "&УсловиеОтбораГруппДоступа3"));
	Поля.Добавить(Новый Структура("ВидДоступа"));
	Поля.Добавить(Новый Структура("ТолькоВидДоступа"));
	Поля.Добавить(Новый Структура("ЗначениеДоступа"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = УправлениеДоступомСлужебный.ТекстЗапросаВыбораИзменений(
		ТекстЗапроса, Поля, "РегистрСведений.ЗначенияГруппДоступа", ТекстЗапросовВременныхТаблиц);
	
	УправлениеДоступомСлужебный.УстановитьУсловиеОтбораВЗапросе(Запрос, ГруппыДоступа, "ГруппыДоступа",
		"&УсловиеОтбораГруппДоступа1:ГруппыДоступа.Ссылка
		|&УсловиеОтбораГруппДоступа2:ВидыДоступа.Ссылка
		|&УсловиеОтбораГруппДоступа3:СтарыеДанные.ГруппаДоступа");
		
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗначенияГруппДоступа");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		Изменения = Запрос.Выполнить().Выгрузить();
		
		УправлениеДоступомСлужебный.ОбновитьРегистрСведений(
			РегистрыСведений.ЗначенияГруппДоступа, Изменения, ЕстьИзменения, , "ГруппаДоступа");
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецЕсли