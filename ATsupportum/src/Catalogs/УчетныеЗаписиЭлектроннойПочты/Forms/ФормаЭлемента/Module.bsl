////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.БлокироватьВладельца Тогда
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Пароль) Тогда
		ЗапомнитьПароль = Истина;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ИспользоватьДляОтправки = Истина;
		Объект.ИспользоватьДляПолучения = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект)
	
	Если НЕ ЗапомнитьПароль Тогда
		ТекущийОбъект.Пароль = "";
	КонецЕсли;
	
	Если Объект.SMTPАутентификация <> Перечисления.ВариантыSMTPАутентификации.ЗадаетсяПараметрами Тогда
		Объект.ПользовательSMTP = "";
		Объект.ПарольSMTP = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// Обрабатывает оповещение, от формы настройки дополнительных свойств учетной записи,
	// содержащее некоторые настройки учетной записи.
	
	Если ИмяСобытия = "УстановкаДополнительныхПараметровУчетнойЗаписи" И Источник = Объект.Ссылка Тогда
		Объект.ВремяОжидания                    = Параметр.ДлительностьОжиданияСервера;
		Объект.ОставлятьКопииСообщенийНаСервере = Параметр.ОставлятьКопииСообщенийНаСервере;
		Объект.ПериодХраненияСообщенийНаСервере = Параметр.ПериодХраненияСообщенийНаСервере;
		Объект.ПользовательSMTP                 = Параметр.ПользовательSMTP;
		Объект.ПарольSMTP                       = Параметр.ПарольSMTP;
		Объект.ПортPOP3                         = Параметр.ПортPOP3;
		Объект.ПортSMTP                         = Параметр.ПортSMTP;
		Объект.SMTPАутентификация               = Параметр.SMTPАутентификация;
		Объект.СпособSMTPАутентификации         = Параметр.СпособSMTPАутентификации;
		Объект.СпособPOP3Аутентификации         = Параметр.СпособPOP3Аутентификации;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Не ПустаяСтрока(Объект.АдресЭлектроннойПочты) И Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Объект.АдресЭлектроннойПочты) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Почтовый адрес заполнен неверно'"), , "АдресЭлектроннойПочты", "Объект");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_УчетныеЗаписиЭлектроннойПочты", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ДополнительныеНастройкиВыполнить()
	
	УчетнаяЗаписьСтруктура = Новый Структура;
	УчетнаяЗаписьСтруктура.Вставить("ВремяОжидания",                    Объект.ВремяОжидания);
	УчетнаяЗаписьСтруктура.Вставить("ОставлятьКопииСообщенийНаСервере", Объект.ОставлятьКопииСообщенийНаСервере);
	УчетнаяЗаписьСтруктура.Вставить("ПериодХраненияСообщенийНаСервере", Объект.ПериодХраненияСообщенийНаСервере);
	УчетнаяЗаписьСтруктура.Вставить("ПользовательSMTP",                 Объект.ПользовательSMTP);
	УчетнаяЗаписьСтруктура.Вставить("ПарольSMTP",                       Объект.ПарольSMTP);
	УчетнаяЗаписьСтруктура.Вставить("ПортPOP3",                         Объект.ПортPOP3);
	УчетнаяЗаписьСтруктура.Вставить("ПортSMTP",                         Объект.ПортSMTP);
	УчетнаяЗаписьСтруктура.Вставить("SMTPАутентификация",               Объект.SMTPАутентификация);
	УчетнаяЗаписьСтруктура.Вставить("СпособSMTPАутентификации",         Объект.СпособSMTPАутентификации);
	УчетнаяЗаписьСтруктура.Вставить("СпособPOP3Аутентификации",         Объект.СпособPOP3Аутентификации);
	
	
	
	ПараметрыВызова = Новый Структура("Ссылка, УчетнаяЗаписьСтруктура, ТолькоПросмотр", Объект.Ссылка, УчетнаяЗаписьСтруктура, ТолькоПросмотр);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.Форма.ДополнительныеСвойстваУчетнойЗаписи", ПараметрыВызова);
	
КонецПроцедуры
