////////////////////////////////////////////////////////////////////////////////
// Подсистема "Дополнительные отчеты и обработки в модели сервиса", расширение
// безопасного режима, служебные процедуры и функции
// 
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Только для внутреннего использования.
Процедура ПолучитьРазрешенияСессииРасширенияБезопасногоРежима(Знач КлючСессии, ОписанияРазрешений, СтандартнаяОбработка) Экспорт
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		УстановитьПривилегированныйРежим(Истина);
		ИспользуемаяОбработка = Справочники.ДополнительныеОтчетыИОбработки.ПолучитьСсылку(КлючСессии);
		Если ДополнительныеОтчетыИОбработкиВМоделиСервиса.ЭтоПоставляемаяОбработка(ИспользуемаяОбработка) Тогда
			
			ПоставляемаяОбработка = ДополнительныеОтчетыИОбработкиВМоделиСервиса.ПоставляемаяОбработка(ИспользуемаяОбработка);
			
			ТекстЗапроса =
				"ВЫБРАТЬ
				|	Разрешения.ВидРазрешения КАК ВидРазрешения,
				|	Разрешения.Параметры КАК Параметры
				|ИЗ
				|	Справочник.ПоставляемыеДополнительныеОтчетыИОбработки.Разрешения КАК Разрешения
				|ГДЕ
				|	Разрешения.Ссылка = &Ссылка";
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Ссылка", ПоставляемаяОбработка);
			ОписанияРазрешений = Запрос.Выполнить().Выгрузить();
			
		Иначе
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не обнаружена поставляемая обработка для ключа запуска %1!'"),
				Строка(КлючСессии));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
