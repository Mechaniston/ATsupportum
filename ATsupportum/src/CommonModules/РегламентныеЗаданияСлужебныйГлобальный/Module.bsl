////////////////////////////////////////////////////////////////////////////////
// Подсистема "Регламентные задания".
// 
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Уведомляет пользователей о невыполнении регламентных заданий и "зависании" фоновых заданий.
//
//  Процедура работает только если информационная база файловая.
//  Для серверной ИБ регламентные задания выполняются сервером 1С:Предприятия.
//
//  Подключается в процедуре РегламентныеЗаданияСлужебныйКлиент.ПриНачалеРаботыСистемы().
//
Процедура УведомлятьОНекорректномВыполненииРегламентныхЗаданий() Экспорт

	ПериодУведомления = Неопределено; // Минут.
	
	Если РегламентныеЗаданияСлужебныйВызовСервера
			.НужноУведомлятьОНекорректномВыполненииРегламентныхЗаданий(ПериодУведомления) Тогда
		
		ОписаниеОшибки = "";
		ЗаданияВыполняютсяНормально = Неопределено;
		
		РегламентныеЗаданияСлужебныйВызовСервера.ТекущийСеансВыполняетРегламентныеЗадания(
			ЗаданияВыполняютсяНормально, , ОписаниеОшибки);
		
		Если НЕ ЗаданияВыполняютсяНормально Тогда
			РегламентныеЗаданияСлужебныйКлиент.ПриОшибкеВыполненияРегламентныхЗаданий(ОписаниеОшибки);
		КонецЕсли;
		ПодключитьОбработчикОжидания("УведомлятьОНекорректномВыполненииРегламентныхЗаданий", ПериодУведомления * 60, Истина);
	КонецЕсли;

КонецПроцедуры

// Выполняет очередное регламентное задание в основном сеансе.
//
// Вызывается обработчиком ожидания, который подключается в
// РегламентныеЗаданияСлужебныйКлиент.ПриНачалеРаботыСистемы().
//
//  Для выполнения в отдельном сеансе применяется форма
// Обработки.РегламентныеИФоновыеЗадания.Форма.ВыполнениеРегламентныхЗаданий.
//
Процедура ВыполнениеРегламентныхЗаданийВОсновномСеансе() Экспорт

	Если РегламентныеЗаданияСлужебныйВызовСервера.ТекущийСеансВыполняетРегламентныеЗадания() Тогда
		РегламентныеЗаданияСлужебныйВызовСервера.ВыполнитьРегламентныеЗадания();
		ПодключитьОбработчикОжидания("ВыполнениеРегламентныхЗаданийВОсновномСеансе", 60, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Запускает отдельный сеанс для выполнения регламентных заданий.
Процедура ЗапуститьОтдельныйСеансДляВыполненияРегламентныхЗаданийЧерезОбработчикОжидания() Экспорт
	
	Результат = РегламентныеЗаданияКлиент.ЗапуститьОтдельныйСеансДляВыполненияРегламентныхЗаданий();
	
	Если Результат.Отказ  Тогда
		Предупреждение(Результат.ОписаниеОшибки);
		
	ИначеЕсли Результат.ВыполненаПопыткаОткрытия Тогда
		
		ПодключитьОбработчикОжидания("АктивизироватьГлавноеОкноТекущегоСеансаПослеЗапускаОтдельногоСеансаВыполненияРегламентныхЗаданий", 2, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Активизирует главное окно текущего сеанса.
Процедура АктивизироватьГлавноеОкноТекущегоСеансаПослеЗапускаОтдельногоСеансаВыполненияРегламентныхЗаданий() Экспорт
	
	ОсновноеОкно = РегламентныеЗаданияСлужебныйКлиент.ОсновноеОкно();
	Если ОсновноеОкно <> Неопределено Тогда
		ОсновноеОкно.Активизировать();
	КонецЕсли;
	
КонецПроцедуры

