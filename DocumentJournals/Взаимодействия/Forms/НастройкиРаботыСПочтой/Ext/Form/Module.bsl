
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОтправлятьПисьмаВФорматеHTML = ПолучитьФункциональнуюОпцию("ОтправлятьПисьмаВФорматеHTML");
	НастройкиХранилище = Взаимодействия.ПолучитьНастройкуРаботаСПочтой();
	
	ВключатьПодписьДляНовыхСообщений             = ?(НастройкиХранилище.Свойство("ВключатьПодписьДляНовыхСообщений"),
		НастройкиХранилище.ВключатьПодписьДляНовыхСообщений,Истина);
	ФорматПодписиДляНовыхСообщений               = ?(НастройкиХранилище.Свойство("ФорматПодписиДляНовыхСообщений") И ОтправлятьПисьмаВФорматеHTML,
		НастройкиХранилище.ФорматПодписиДляНовыхСообщений,
		Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст);
	ВключатьПодписьПриОтветеПересылке            = ?(НастройкиХранилище.Свойство("ВключатьПодписьПриОтветеПересылке"),
		НастройкиХранилище.ВключатьПодписьПриОтветеПересылке,Истина);
	ФорматПодписиПриОтветеПересылке              = ?(НастройкиХранилище.Свойство("ФорматПодписиПриОтветеПересылке") И ОтправлятьПисьмаВФорматеHTML,
		НастройкиХранилище.ФорматПодписиПриОтветеПересылке,
		Перечисления.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст);
	ПорядокОтветовНаЗапросыУведомленийОПрочтении = ?(НастройкиХранилище.Свойство("ПорядокОтветовНаЗапросыУведомленийОПрочтении"),
		НастройкиХранилище.ПорядокОтветовНаЗапросыУведомленийОПрочтении,
		Перечисления.ПорядокОтветовНаЗапросыУведомленийОПрочтении.ЗапрашиватьПередТемКакОтправитьУведомление);
	ВсегдаЗапрашиватьУведомлениеОПрочтении       = ?(НастройкиХранилище.Свойство("ВсегдаЗапрашиватьУведомлениеОПрочтении"),
		НастройкиХранилище.ВсегдаЗапрашиватьУведомлениеОПрочтении,Ложь);
	ВсегдаЗапрашиватьУведомленияОДоставке        = ?(НастройкиХранилище.Свойство("ВсегдаЗапрашиватьУведомленияОДоставке"),
		НастройкиХранилище.ВсегдаЗапрашиватьУведомленияОДоставке,Ложь);
	НовоеСообщениеФорматированныйДокумент        = ?(НастройкиХранилище.Свойство("НовоеСообщениеФорматированныйДокумент"),
		НастройкиХранилище.НовоеСообщениеФорматированныйДокумент,Неопределено);
	ПодписьДляНовыхСообщенийПростойТекст         = ?(НастройкиХранилище.Свойство("ПодписьДляНовыхСообщенийПростойТекст"),
		НастройкиХранилище.ПодписьДляНовыхСообщенийПростойТекст,Неопределено);
	ПодписьПриОтветеПересылкеПростойТекст        = ?(НастройкиХранилище.Свойство("ПодписьПриОтветеПересылкеПростойТекст"),
		НастройкиХранилище.ПодписьПриОтветеПересылкеПростойТекст,Неопределено);
	ПриОтветеПересылкеФорматированныйДокумент    = ?(НастройкиХранилище.Свойство("ПриОтветеПересылкеФорматированныйДокумент"),
		НастройкиХранилище.ПриОтветеПересылкеФорматированныйДокумент,Неопределено);
	ОтображатьТелоИсходногоПисьма                = ?(НастройкиХранилище.Свойство("ОтображатьТелоИсходногоПисьма"),
		НастройкиХранилище.ОтображатьТелоИсходногоПисьма,Ложь);
	ВключатьТелоИсходногоПисьма                  = ?(НастройкиХранилище.Свойство("ВключатьТелоИсходногоПисьма"),
		НастройкиХранилище.ВключатьТелоИсходногоПисьма,Ложь);
	ОтправлятьСообщенияСразу                     = ?(НастройкиХранилище.Свойство("ОтправлятьСообщенияСразу"),
		НастройкиХранилище.ОтправлятьСообщенияСразу,Ложь);
		
	// + ат
	УчетнаяЗаписьПоУмолчанию					 = ?(НастройкиХранилище.Свойство("УчетнаяЗаписьПоУмолчанию"),
		НастройкиХранилище.УчетнаяЗаписьПоУмолчанию, Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка());
	// - ат
		
	Элементы.ОтправлятьСообщенияСразу.Видимость = ОбщегоНазначения.ИнформационнаяБазаФайловая();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностью();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВключатьПодписьДляНовыйСообщенийПриИзменении(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключатьПодписьПриОтветеПересылкеПриИзменении(Элемент)
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматПодписиДляНовыхСообщенийПриИзменении(Элемент)
	
	Если ФорматПодписиДляНовыхСообщений = 
			ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") И
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница = 
			Элементы.СтраницаНовоеСообщениеФорматированныйТекст Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ФорматПодписиДляНовыхСообщений = 
			ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст") И
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница = Элементы.СтраницаНовоеСообщениеПростойТекст Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ФорматПодписиДляНовыхСообщений = 
			ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Если Не ПустаяСтрока(ПодписьДляНовыхСообщенийПростойТекст) Тогда
			НовоеСообщениеФорматированныйДокумент.Удалить();
			НовоеСообщениеФорматированныйДокумент.Добавить(ПодписьДляНовыхСообщенийПростойТекст);
		КонецЕсли;
		
	Иначе
		
		Если Не ВзаимодействияКлиент.ПриИзмененииФорматаСообщенияНаОбычныйТекст() Тогда
			ФорматПодписиДляНовыхСообщений = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML");
			Возврат;
		КонецЕсли;
		
		ПодписьДляНовыхСообщенийПростойТекст = НовоеСообщениеФорматированныйДокумент.ПолучитьТекст();
		НовоеСообщениеФорматированныйДокумент.Удалить();
		
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматПодписиПриОтветеПересылкеПриИзменении(Элемент)
	
	Если ФорматПодписиПриОтветеПересылке = 
			ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") И
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = Элементы.СтраницаПриОтветеПересылкеФорматированныйДокумент Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ФорматПодписиПриОтветеПересылке =
			ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.ОбычныйТекст") И
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = Элементы.СтраницаПриОтветеПересылкеПростойТекст Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ФорматПодписиПриОтветеПересылке = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Если Не ПустаяСтрока(ПодписьПриОтветеПересылкеПростойТекст) Тогда
			ПриОтветеПересылкеФорматированныйДокумент.Удалить();
			ПриОтветеПересылкеФорматированныйДокумент.Добавить(ПодписьПриОтветеПересылкеПростойТекст);
		КонецЕсли;
		
	Иначе
		
		Если Не ВзаимодействияКлиент.ПриИзмененииФорматаСообщенияНаОбычныйТекст() Тогда
			ФорматПодписиПриОтветеПересылке = ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML");
			Возврат;
		КонецЕсли;
		
		ПодписьПриОтветеПересылкеПростойТекст = ПриОтветеПересылкеФорматированныйДокумент.ПолучитьТекст();
		ПриОтветеПересылкеФорматированныйДокумент.Удалить();
		
	КонецЕсли;
	
	УправлениеДоступностью();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СохранитьНастройки();
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УправлениеДоступностью()

	Если ФорматПодписиДляНовыхСообщений = 
			ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница = Элементы.СтраницаНовоеСообщениеФорматированныйТекст;
		Элементы.НовоеСообщениеФорматированныйДокумент.Доступность = ВключатьПодписьДляНовыхСообщений;
		
	Иначе
		
		Элементы.СтраницыПодписьДляНовыхСообщений.ТекущаяСтраница = Элементы.СтраницаНовоеСообщениеПростойТекст;
		Элементы.ПодписьДляНовыхСообщенийПростойТекст.Доступность = ВключатьПодписьДляНовыхСообщений;
		
	КонецЕсли;
	
	Если ФорматПодписиПриОтветеПересылке = 
			ПредопределенноеЗначение("Перечисление.СпособыРедактированияЭлектронныхПисем.HTML") Тогда
		
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = 
			Элементы.СтраницаПриОтветеПересылкеФорматированныйДокумент;
		Элементы.ПриОтветеПересылкеФорматированныйДокумент.Доступность  = ВключатьПодписьПриОтветеПересылке;
		
	Иначе
		
		Элементы.СтраницыПодписьПриОтветеПересылке.ТекущаяСтраница = Элементы.СтраницаПриОтветеПересылкеПростойТекст;
		Элементы.ПодписьПриОтветеПересылкеПростойТекст.Доступность  = ВключатьПодписьПриОтветеПересылке;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Если ФорматПодписиДляНовыхСообщений <> Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		ПодписьДляНовыхСообщенийФорматированныйДокумент = Неопределено;
		
	Иначе
		
		ПодписьДляНовыхСообщенийПростойТекст = НовоеСообщениеФорматированныйДокумент.ПолучитьТекст();
		
	КонецЕсли;
	
	Если ФорматПодписиПриОтветеПересылке <> Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		
		ПодписьПриОтветеПересылкеФорматированныйДокумент = Неопределено;
		
	Иначе
		
		ПодписьПриОтветеПересылкеПростойТекст = ПриОтветеПересылкеФорматированныйДокумент.ПолучитьТекст();
		
	КонецЕсли;
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("ВключатьПодписьДляНовыхСообщений", ВключатьПодписьДляНовыхСообщений);
	СтруктураНастройки.Вставить("ВключатьПодписьПриОтветеПересылке", ВключатьПодписьПриОтветеПересылке);
	СтруктураНастройки.Вставить("ВсегдаЗапрашиватьУведомлениеОПрочтении", ВсегдаЗапрашиватьУведомлениеОПрочтении);
	СтруктураНастройки.Вставить("ВсегдаЗапрашиватьУведомленияОДоставке", ВсегдаЗапрашиватьУведомленияОДоставке);
	СтруктураНастройки.Вставить("НовоеСообщениеФорматированныйДокумент", НовоеСообщениеФорматированныйДокумент);
	СтруктураНастройки.Вставить("ПодписьДляНовыхСообщенийПростойТекст", ПодписьДляНовыхСообщенийПростойТекст);
	СтруктураНастройки.Вставить("ПодписьПриОтветеПересылкеПростойТекст", ПодписьПриОтветеПересылкеПростойТекст);
	СтруктураНастройки.Вставить("ПорядокОтветовНаЗапросыУведомленийОПрочтении", ПорядокОтветовНаЗапросыУведомленийОПрочтении);
	СтруктураНастройки.Вставить("ПриОтветеПересылкеФорматированныйДокумент", ПриОтветеПересылкеФорматированныйДокумент);
	СтруктураНастройки.Вставить("ФорматПодписиДляНовыхСообщений", ФорматПодписиДляНовыхСообщений);
	СтруктураНастройки.Вставить("ФорматПодписиПриОтветеПересылке", ФорматПодписиПриОтветеПересылке);
	СтруктураНастройки.Вставить("ОтображатьТелоИсходногоПисьма", ОтображатьТелоИсходногоПисьма);
	СтруктураНастройки.Вставить("ВключатьТелоИсходногоПисьма", ВключатьТелоИсходногоПисьма);
	СтруктураНастройки.Вставить("ОтправлятьСообщенияСразу", ОтправлятьСообщенияСразу);
	
	// + ат
	
	СтруктураНастройки.Вставить("УчетнаяЗаписьПоУмолчанию", УчетнаяЗаписьПоУмолчанию);
	
	// - ат
	
	Взаимодействия.СохранитьНастройкуРаботаСПочтой(СтруктураНастройки);

КонецПроцедуры

